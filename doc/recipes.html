<!DOCTYPE html>
<html lang="en"><head><link href="tufte.css" rel="stylesheet" type="text/css"><link href="speculoos.css" rel="stylesheet" type="text/css"><title>Speculoos — Recipes</title><meta charset="utf-8" compile-date="2024-07-24 09:05:20" content="width=device-width, initial-scale=1" name="viewport"><body><nav id="nav-bar"><ul><li><a href="home.html">Home</a></li><li><a href="ideas.html">Ideas</a></li><li><a href="documentation.html">Documentation</a></li><li>Recipes</li><li><a href="diff.html"><code>diff</code></a></li><li><a href="pros_cons.html">Pros, Cons, &amp; Alts</a></li><li class="small-caps"><a href="index.html">api</a></li><li><a href="source.html">Source</a></li><li><a href="contact.html">Contact</a></li></ul></nav><article><h1>Speculoos Recipes</h1><section id="intro"><p>Example usage, not too many words.</p></section><section class="nav-section"><p><a href="#valid-vs-validation">Valid versus Validation</a><br><a href="#scalar-validation">Scalar validation</a><br><a href="#collection-validation">Collection validation</a><br><a href="#comprehensive-validation">Comprehensive validation</a><br><a href="#function-validation">Function validation</a><br><a href="#fn-in"><code>fn*</code> and <code>fn-in*</code></a><br><a href="#utility">Utility functions</a><br><a href="#predicates">Predicates</a><br></p></section><section id="valid-vs-validation"><h2>Valid versus Validation</h2><p><em>Validation</em> is the process of systematically applying predicates to scalars and collections. The validation process returns an exhaustive sequence of every datum-predicate pair and the truthy/falsey result of the validation.</p><pre><code>(require '[speculoos.core :refer
           [validate-scalars valid-scalars? only-valid
            only-invalid]])</code><br><code>(validate-scalars [42 :foo "abc"] [int? keyword? boolean?])
;; => [{:datum 42,
;;      :path [0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:datum :foo,
;;      :path [1],
;;      :predicate #function [clojure.core/keyword?],
;;      :valid? true}
;;     {:datum "abc",
;;      :path [2],
;;      :predicate #function [clojure.core/boolean?],
;;      :valid? false}]</code></pre><p>Speculoos provides some helper functions to filter the results.</p><pre><code>(only-valid (validate-scalars [42 :foo "abc"]
                              [int? keyword? boolean?]))
;; => ({:datum 42,
;;      :path [0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:datum :foo,
;;      :path [1],
;;      :predicate #function [clojure.core/keyword?],
;;      :valid? true})</code><br><code>(only-invalid (validate-scalars [42 :foo "abc"]
                                [int? keyword? boolean?]))
;; => ({:datum "abc",
;;      :path [2],
;;      :predicate #function [clojure.core/boolean?],
;;      :valid? false})</code></pre><p><em>Valid</em> is a high level <code>true/false</code> summary of a validation. If the <code>only-invalids</code> sequence is empty, then <code>valid?</code> is <code>true</code>; <code>false</code> otherwise.</p><pre><code>(valid-scalars? [42 :foo "abc"] [int? keyword? string?])
;; => true</code><br><code>(valid-scalars? [42 :foo "abc"] [int? keyword? boolean?])
;; => false</code></pre></section><section id="scalar-validation"><h2>Scalar validation</h2><p>Scalar validation applies predicates to basic Clojure datums like numbers, strings, booleans, characters, keywords, etc.</p><h3>Empty collections</h3><p>Speculoos can validate in the absence of scalars. Predicates are simply ignored.</p><pre><code>(validate-scalars [] [int?]) ;; => []</code><code>(validate-scalars '() [int?]) ;; => []</code><code>(validate-scalars {} [int?]) ;; => []</code><code>(validate-scalars #{} [int?]) ;; => []</code></pre><h3>Empty specifications</h3><p>Validating may also proceed without predicates. Not much happens.</p><pre><code>(validate-scalars [42 :foo "abc"] []) ;; => []</code><code>(validate-scalars '(42 :foo "abc") []) ;; => []</code><code>(validate-scalars {:a 42, :b :foo, :c "abc"} []) ;; => []</code><code>(validate-scalars #{42 :foo "abc"} []) ;; => []</code></pre><h3>Vectors</h3><p>Speculoos validates a vector's scalars in order.</p><pre><code>(valid-scalars? [42] [int?]) ;; => true</code><code>(valid-scalars? [42 :foo] [int? keyword?]) ;; => true</code><code>(valid-scalars? [42 :foo "abc"] [int? keyword? string?])
;; => true</code><br><code>(only-invalid (validate-scalars [42 :foo "abc"]
                                [int? keyword? boolean?]))
;; => ({:datum "abc",
;;      :path [2],
;;      :predicate #function [clojure.core/boolean?],
;;      :valid? false})</code></pre><h3>Lists</h3><p>Speculoos validates a list's scalars in order.</p><pre><code>(valid-scalars? (list 42) (list int?)) ;; => true</code><code>(valid-scalars? (list 42 :foo) (list int? keyword?))
;; => true</code><code>(valid-scalars? (list 42 :foo "abc")
                (list int? keyword? string?))
;; => true</code><br><code>(only-invalid (validate-scalars
                (list 42 :foo "abc")
                (list int? keyword? boolean?)))
;; => ({:datum "abc",
;;      :path [2],
;;      :predicate #function [clojure.core/boolean?],
;;      :valid? false})</code></pre><h3>Maps</h3><p>Speculoos validates a map's scalars by key.</p><pre><code>(valid-scalars? {:a 42} {:a int?}) ;; => true</code><code>(valid-scalars? {:a 42, :b 'foo} {:a int?, :b symbol?})
;; => true</code><br><code>(valid-scalars? {:a 42, :b 'foo, :c "abc"}
                {:a int?, :b symbol?, :c string?})
;; => true</code><br><code>(valid-scalars? {:a 42, :b 'foo, :c "abc"}
                {:b symbol?, :c string?, :a int?})
;; => true</code><br><code>(only-invalid (validate-scalars
                {:a 42, :b 'foo, :c "abc"}
                {:a int?, :b boolean?, :c string?}))
;; => ({:datum foo,
;;      :path [:b],
;;      :predicate #function [clojure.core/boolean?],
;;      :valid? false})</code></pre><h3>Sets</h3><p>Speculoos validates a set's scalars by applying <em>all</em> predicates to <em>every</em> scalar. If all predicates return <code>true</code> for every scalar, then the data and specification are valid. If any scalar does not satisfy a single predicate, then it's invalid.</p><pre><code>(valid-scalars? #{42} #{int?}) ;; => true</code><code>(valid-scalars? #{42 0 99} #{int?}) ;; => true</code><code>(valid-scalars? #{42 'foo 99} #{int?}) ;; => false</code><br><code>(valid-scalars? #{42} #{int? number? pos?}) ;; => true</code><code>(valid-scalars? #{42} #{int? string?}) ;; => false</code><code>(valid-scalars? #{42 99 -11} #{int? number? pos?})
;; => false</code><br><code>(defn int-or-str? [x] ((some-fn int? string?) x))</code><br><code>(valid-scalars? #{42 "abc" 99} #{int-or-str?}) ;; => true</code></pre><h3>Non-terminating sequences</h3><p>Speculoos can handle non-terminating sequences in two situations. First, Speculoos can validate a non-terminating sequence of scalars as long as the corresponding specification is not also a non-terminating sequence. Validation will proceed until the predicates run out.</p><pre><code>(valid-scalars? (repeat 42) [int?]) ;; => true</code><code>(valid-scalars? (repeat 42) [int? int? int?]) ;; => true</code><br><code>(only-invalid (validate-scalars (repeat 42)
                                [int? string? int?]))
;; => ({:datum 42,
;;      :path [1],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? false})</code><br><code>(valid-scalars? (cycle [42 :foo "abc"]) [int?]) ;; => true</code><br><code>(valid-scalars? (cycle [42 :foo "abc"])
                [int? keyword? string?])
;; => true</code><br><code>(only-invalid (validate-scalars (cycle [42 :foo "abc"])
                                [int? boolean? string?]))
;; => ({:datum :foo,
;;      :path [1],
;;      :predicate #function [clojure.core/boolean?],
;;      :valid? false})</code><br><code>(valid-scalars? (range) [int?]) ;; => true</code><code>(valid-scalars? (range) [int? int? int?]) ;; => true</code><code>(valid-scalars? (range) [int? boolean? int?]) ;; => false</code><br><code>(valid-scalars? (iterate inc 1) [int?]) ;; => true</code><code>(valid-scalars? (iterate inc 1) [int? int? int?]) ;; => true</code><br><code>(valid-scalars? (iterate inc 1) [int? boolean? int?])
;; => false</code><br><code>(valid-scalars? (lazy-seq [1 2 3]) [int?]) ;; => true</code><code>(valid-scalars? (lazy-seq [1 2 3]) [int? int? int?])
;; => true</code><br><code>(valid-scalars? (lazy-seq [1 2 3]) [int? boolean? int?])
;; => false</code></pre><p>Second, Speculoos can use a specification that is composed of a non-terminating sequence as long as the data is not also a non-terminating sequence.</p><pre><code>(valid-scalars? [42] (repeat int?)) ;; => true</code><code>(valid-scalars? [42 0 99] (repeat int?)) ;; => true</code><br><code>(valid-scalars? [42 :foo "abc"]
                (cycle [int? keyword? string?]))
;; => true</code><br><code>(valid-scalars? [42 :foo "abc"]
                (cycle [int? boolean? string?]))
;; => false</code></pre><h3>Nested collections</h3><p>Speculoos can validate any scalar in a heterogeneous, arbitrarily-nested data structure. The scalar and predicate share a path into the data and specification, respectively. Another way to think about it is that the specification mimics the data's structure, and the predicate and scalar are located at the same position within each.</p><p>Examples of homogeneous, nested collections.</p><pre><code>(valid-scalars? [42 [:foo] "abc"] [int? [keyword?] string?])
;; => true</code><br><code>(valid-scalars? [42 [:foo ["abc"]]]
                [int? [keyword? [string?]]])
;; => true</code><br><code>(valid-scalars? [[42] [:foo] ["abc"]]
                [[int?] [keyword?] [string?]])
;; => true</code><br><code>(valid-scalars? (list 42 (list :foo (list "abc")))
                (list int? (list keyword? (list string?))))
;; => true</code><br><code>(valid-scalars? {:a 42, :b {:c 'foo, :d "abc"}}
                {:a int?, :b {:c symbol?, :d string?}})
;; => true</code><br><code>(valid-scalars? {:a 42, :b {:c {:d 'foo, :e "abc"}}}
                {:a int?, :b {:c {:d symbol?, :e string?}}})
;; => true</code></pre><p>Examples of heterogeneous, nested collections.</p><pre><code>(validate-scalars [42 {:a 'foo, :b "abc"}]
                  [int? {:a symbol?, :b string?}])
;; => [{:datum 42,
;;      :path [0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:datum foo,
;;      :path [1 :a],
;;      :predicate #function [clojure.core/symbol?],
;;      :valid? true}
;;     {:datum "abc",
;;      :path [1 :b],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? true}]</code><br><code>(validate-scalars [42 {:a ['foo {:b (list "abc")}]}]
                  [int? {:a [symbol? {:b (list string?)}]}])
;; => [{:datum 42,
;;      :path [0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:datum foo,
;;      :path [1 :a 0],
;;      :predicate #function [clojure.core/symbol?],
;;      :valid? true}
;;     {:datum "abc",
;;      :path [1 :a 1 :b 0],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? true}]</code><br><code>(validate-scalars [[[[42]]] [[[:foo]]] [[["abc"]]]]
                  [[[[int?]]] [[[keyword?]]] [[[string?]]]])
;; => [{:datum 42,
;;      :path [0 0 0 0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:datum :foo,
;;      :path [1 0 0 0],
;;      :predicate #function [clojure.core/keyword?],
;;      :valid? true}
;;     {:datum "abc",
;;      :path [2 0 0 0],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? true}]</code></pre><p>Scalars contained in a nested set can be validated.</p><pre><code>(valid-scalars? [#{42 0 99} #{'foo} #{:foo :bar :baz}]
                [#{int?} #{symbol?} #{keyword?}])
;; => true</code></pre><h3>Adjusting specifications</h3><p>What if the specification in your hand is close, but not exactly, what you need? You can relax a specification with a couple of strategies. First strategy: Speculoos will only validate if it has a predicate, so you could simply <em>remove</em> the offending predicate.</p><pre><code>(valid-scalars? [42 :foo "abc"] [int? keyword? boolean?])
;; => false</code><br><code>(valid-scalars? [42 :foo "abc"]
                (pop [int? keyword? boolean?]))
;; => true</code><br><code>(valid-scalars? {:a 42, :b 'foo} {:a int?, :b boolean?})
;; => false</code><br><code>(valid-scalars? {:a 42, :b 'foo}
                (dissoc {:a int?, :b boolean?} :b))
;; => true</code></pre><p>Second strategy: <em>Replace</em> the offending predicate with a more permissive predicate.</p><pre><code>(valid-scalars? [42 :foo "abc"] [int? keyword? boolean?])
;; => false</code><br><code>(valid-scalars? [42 :foo "abc"] [int? keyword? any?])
;; => true</code><br><code>(valid-scalars? {:a 42, :b 'foo} {:a int?, :b boolean?})
;; => false</code><br><code>(valid-scalars? {:a 42, :b 'foo}
                (assoc {:a int?, :b boolean?} :b any?))
;; => true</code></pre><p>If you need to dive into a nested structure to adjust the specification, Speculoos has a <a href="#fn-in">few functions</a> that'll help. Speculoos also provides a few <a href="#utility">utilities</a> specifically for adjusting specifications.</p><h3>Repairing data</h3><p>What if you have a specification, and some data that almost satisfies it? You could adjust the data so that it satisfies the specification. The crude way would be to remove the offending datum.</p><pre><code>(valid-scalars? {:a 42, :b 'foo} {:a int?, :b string?})
;; => false</code><br><code>(valid-scalars? (dissoc {:a 42, :b 'foo} :b)
                {:a int?, :b string?})
;; => true</code></pre><p>Another option involves associating a new value to satisfy the predicate.</p><pre><code>(valid-scalars? (assoc {:a 42, :b 'foo} :b "foo")
                {:a int?, :b string?})
;; => true</code></pre><h3>Optionality</h3><p>Speculoos provides a handful of ways to indicate optionality, depending on the situation. Speculoos does not infer nor assume; it only validates pairs of datums and predicates. Supplying predicates to a specification says <em>These datums may or may not exist. If the datums exist, they must satisfy these predicates. If the datums do not exist, the predicates are ignored.</em></p><pre><code>(valid-scalars? [42 :foo]
                [int? keyword? string? boolean? ratio?
                 char?])
;; => true</code><br><code>(valid-scalars? [42 :foo "abc" true]
                [int? keyword? string? boolean? ratio?
                 char?])
;; => true</code><br><code>(valid-scalars? {:a 42, :b 'foo}
                {:a int?,
                 :b symbol?,
                 :c string?,
                 :d boolean?,
                 :e ratio?,
                 :f char?})
;; => true</code><br><code>(valid-scalars? {:a 42, :b 'foo, :d true, :f \c}
                {:a int?,
                 :b symbol?,
                 :c string?,
                 :d boolean?,
                 :e ratio?,
                 :f char?})
;; => true</code></pre><p>Within a sequence, if you'd like to validate some of the values, but skip over others, you have a few options to maintain the correspondence between the data and specification: <code>any?</code>, <code>(constantly true)</code>, or <code>(fn [_] true)</code>.</p><pre><code>(valid-scalars? [42 :foo "abc"] [int? any? string?])
;; => true</code><br><code>(valid-scalars? [42 :foo "abc"]
                [int? (constantly true) string?])
;; => true</code><br><code>(valid-scalars? [42 :foo "abc"]
                [int? (fn [_] true) string?])
;; => true</code></pre><p>The results are the same. Pick the option that best signals your intent.</p><p>Maps are easier: Any key in both the specification and the data will be validated. Otherwise, unpaired datums or predicates are ignored.</p><pre><code>(valid-scalars? {:validated 42, :ignored-datum 'foo}
                {:validated int?, :ignored-predicate char?})
;; => true</code></pre><p>Considering a single datum, you can bestow optionality by composing a predicate with <code>or</code>, or, my preference, <a href="https://clojure.github.io/clojure/clojure.core-api.html#clojure.core/some-fn"><code>clojure.core/some-fn</code></a>.</p><pre><code>(valid-scalars? [42 :foo "abc"]
                [int? #(or (symbol? %) (keyword? %))
                 (some-fn char? string?)])
;; => true</code></pre></section><section id="collection-validation"><h2>Collection validation</h2><p>Collection validation applies predicates to Clojure data structures like vectors, lists, maps, sets, lazy sequences, etc.</p><h3>Empty specifications</h3><p>Speculoos will consume an empty specification, and return.</p><pre><code>(require '[speculoos.core :refer
           [validate-collections valid-collections?]])</code><br><code>(validate-collections [] []) ;; => ()</code><code>(validate-collections '() '()) ;; => ()</code><code>(validate-collections {} {}) ;; => ()</code><code>(validate-collections #{} #{}) ;; => ()</code></pre><p>There is nothing particularly special about validating an empty collection. In fact, that's a pretty useful task.</p><pre><code>(valid-collections? [] [empty?]) ;; => true</code><code>(valid-collections? '() '(empty?)) ;; => true</code><code>(valid-collections? {} {:s empty?}) ;; => true</code><code>(valid-collections? #{} #{empty?}) ;; => true</code></pre><h3 id="coll-vec">Vectors</h3><p>Collection predicates apply to the vectors that contain them.</p><pre><code>(valid-collections? [99 []] [(complement empty?) [empty?]])
;; => true</code><br><code>(valid-collections? [1 2 3] [#(= 3 (count %))]) ;; => true</code><br><code>(defn length-1? [v] (= 1 (count v)))</code><br><code>(valid-collections? [[1] [2] [3]]
                    [[length-1?] [length-1?] [length-1?]])
;; => true</code></pre><p>Extending that last example: intervening collection predicates all apply to the root, parent vector.</p><pre><code>(valid-collections? [[1] [2] [3]]
                    [vector? [length-1?] (complement empty?)
                     [length-1?] any? [length-1?]])
;; => true</code><br><code>(valid-collections? [[1] [2] [3]]
                    [[length-1?] vector? (complement empty?)
                     [length-1?] any? [length-1?]])
;; => true</code><br><code>(valid-collections? [[1] [2] [3]]
                    [[length-1?] [length-1?] any?
                     (complement empty?) vector?
                     [length-1?]])
;; => true</code></pre><p>As seen above, any number of predicates apply to their parent collection, and in any order.</p><h3>Lists</h3><p>Speculoos validates lists exactly the same as vectors, so <a href="#coll-vec">everything above</a> holds for lists. A few quick examples.</p><pre><code>(valid-collections? '() '(list?)) ;; => true</code><br><code>(valid-collections? '(1 2 3) '(#(= 3 (count %)))) ;; => true</code><br><code>(valid-collections? '((1) (2) (3))
                    '((length-1?) (length-1?) (length-1?)))
;; => true</code><br><code>(valid-collections?
  '((1) (2) (3))
  '((length-1?) list? (length-1?) (length-1?)))
;; => true</code></pre><h3>Maps</h3><p>Collection predicates apply to the maps that contain them, as long as the key does not appear in the data's corresponding map. Any number of predicates may be supplied. Since the specification's keys don't matter, you can name them something informative.</p><pre><code>(valid-collections? {:a 42} {:map-spec empty?}) ;; => false</code><br><code>(valid-collections? {:a 42} {:coll-test map?}) ;; => true</code><br><code>(valid-collections? {:a 42}
                    {:coll-test map?,
                     :coll-size #(= 1 (count %))})
;; => true</code><br><code>(valid-collections? {:a 42} {:has-key? #(contains? % :a)})
;; => true</code><br><code>(valid-collections? {:a 42, :b 'foo, :c 99}
                    {:all-vals-ints? #(every? int?
                                              (vals %))})
;; => false</code></pre><h3>Sets</h3><p>Predicates within a set apply to the set itself as a whole.</p><pre><code>(valid-collections? #{1 2 3} #{#(every? int? %)}) ;; => true</code><br><code>(valid-collections? #{1 2 :foo} #{#(every? int? %)})
;; => false</code><br><code>(valid-collections? #{1 2 3}
                    #{#(every? int? %) #(every? pos? %)})
;; => true</code><br><code>(valid-collections? #{1 2 3}
                    #{#(every? int? %) #(every? even? %)})
;; => false</code></pre><p>All elements not something.</p><pre><code>(valid-collections? #{1 2 3}
                    #{#(every? (complement keyword?) %)})
;; => true</code></pre><p>For every vector contained in a set, does it contain one integer?</p><pre><code>(valid-collections?
  #{:a [1] :b [42] :c [99]}
  #{(fn [s] (every? #(= 1 (count %)) (filter vector? s)))})
;; => true</code><br><code>(valid-collections?
  #{:a [1 2 3] :b}
  #{(fn [s] (every? #(= 1 (count %)) (filter vector? s)))})
;; => false</code></pre><h3>Non-terminating Sequences</h3><p>You may supply a (possibly) non-terminating sequence in the data, or in the specification, but not both. During collection validation, the non-terminating sequence is clamped to a length of the other corresponding sequence.</p><pre><code>(set! *print-length* 99) ;; => 99</code><br><code>(valid-collections? (range 99) [#(every? int? %)])
;; => true</code><br><code>(valid-collections? (range 99) [#(every? ratio? %)])
;; => false</code><br><code>(valid-collections?
  (cycle [42 :foo "abc"])
  [#(valid-scalars? % [int? keyword? string?])])
;; => true</code><br><code>(valid-collections?
  (iterate #(+ 3 %) 1)
  [(fn [s] (every? true? (map #(= (+ 3 %1) %2) s (next s))))
   (constantly true) (constantly true)])
;; => true</code><br><code>(valid-collections? (repeat :na)
                    [(fn [s] (every? #(= :na %) s))
                     (constantly true) (constantly true)])
;; => true</code></pre><h3>Nested collections</h3><p>Same rule, recursively: predicates apply to their parent collection.</p><pre><code>(valid-collections? [[1] [:a] ['foo]]
                    [[#(every? int? %)]
                     [#(every? keyword? %)]
                     [#(every? symbol? %)]])
;; => true</code><br><code>(valid-collections? [[1 22/7] [:a] ['foo]]
                    [[#(every? int? %)]
                     [#(every? keyword? %)]
                     [#(every? symbol? %)]])
;; => false</code><br><code>(valid-collections? [11 [22 33 44]]
                    [vector? [#(= 3 (count %))]
                     #(= 2 (count %))])
;; => true</code><br><code>(valid-collections?
  {:a 11, :b {:c 22}}
  {:has-keys? #(and (contains? % :a) (contains? % :b)),
   :b {:is-map? map?, :one-MapEntry? #(= 1 (count %))}})
;; => true</code><br><code>(valid-collections?
  (list 11 (list 22 33))
  (list list? (list #(<= 2 (count %))) #(= 2 (count %))))
;; => true</code></pre><p>Heterogeneous, nested collections work the same way.</p><pre><code>(valid-collections?
  [{:a 1, :b 2} 33 {:d 4, :e 5} 66]
  [#(= 2 (count (filter map? %))) {:has? #(contains? % :b)}
   {:has? #(contains? % :d)}
   (fn [v]
     (every? int? (filter #((complement map?) %) v)))])
;; => true</code><br><code>(valid-collections?
  {:a [1 2 3], :b (list \z \q \w), :c {:x "abc", :y 42}}
  {:expected-count? #(= 3 (count %)),
   :a {:type vector?, :length #(<= 2 (count %))},
   :c {:contains? #(contains? % :x)},
   :b {:kind list?,
       :all-same? (fn [s] (apply = (map type s)))}})
;; => true</code><br><code>(valid-collections? [[[[]]]] [[[[empty?]]]]) ;; => true</code><br><code>(valid-collections? {:a {:b {:c {:d {}}}}}
                    {:a {:b {:c {:d {:contains-anything
                                       empty?}}}}})
;; => true</code></pre><h3>Sequence regexes</h3><p>Replicating <code>spec.alpha</code>'s regular expression facilities.</p><p>One or more integers, followed by zero or more strings.</p><pre><code>(defn int+string*
  [s]
  (let [[front rear] (split-with int? s)
        ints #(<= 1 (count front))
        strs (every? string? rear)]
    (and ints strs)))</code><br><code>(int+string* [1 2 3 "a" "b"]) ;; => true</code><code>(int+string* [1 2 3]) ;; => true</code><code>(int+string* [1 2 3 'a 'b]) ;; => false</code><br><code>(valid-collections? [1 2 3 "a" "b"] [int+string*])
;; => true</code><br><code>(valid-collections? [1 2 3 'a 'b] [int+string*]) ;; => false</code></pre><p>Exactly three keywords, followed by anything.</p><pre><code>(defn three-kw-and?
  [s]
  (= 3 (count (take-while keyword? s))))</code><br><code>(valid-collections? [:a :b :c] [three-kw-and?]) ;; => true</code><br><code>(valid-collections? [:a :b] [three-kw-and?]) ;; => false</code><br><code>(valid-collections? [:a :b :c 1 2 3] [three-kw-and?])
;; => true</code></pre><p><code>seq-regex</code>, a little toy utility that streamlines this task. One or more integers, followed by zero or more strings.</p><pre><code>(require '[speculoos.utility :refer [seq-regex]])</code><br><code>(valid-collections? [1 2 3 "a" "b" "c"]
                    [#(seq-regex % int? :+ string? :*)])
;; => true</code></pre><p>Exactly three keywords, followed by anything.</p><pre><code>(valid-collections? [:a :b :c 'foo "bar" \z]
                    [#(seq-regex % keyword? 3 any? :*)])
;; => true</code></pre><p>See also <a href="https://github.com/miner/herbert">Herbert</a> and <a href="https://github.com/cgrand/seqexp">Seqexp</a>.</p><h3>Adjusting specifications</h3><p>Speculoos specifications are just Clojure data structures, containing Clojure predicates. Adjust specifications with your favorite tools. <code>clojure.core</code> provides some nice ones.</p><pre><code>(valid-collections? [[11] [22] [33]]
                    [[#(every? int? %)] [#(= 1 (count %))]
                     [#(not-empty %)]])
;; => false</code><br><code>(valid-collections? [[11] [22] [33]]
                    (drop-last [[#(every? int? %)]
                                [#(= 1 (count %))]
                                [#(not-empty %)]]))
;; => true</code><br><code>(valid-collections? {:a [1 2 3], :b ['foo 'bar 'baz]}
                    {:a [#(every? int? %)],
                     :b [#(every? int? %)]})
;; => false</code><br><code>(valid-collections? {:a [1 2 3], :b ['foo 'bar 'baz]}
                    (assoc-in {:a [#(every? int? %)],
                               :b [#(every? int? %)]}
                      [:b 0]
                      #(every? symbol? %)))
;; => true</code></pre><p>Speculoos provides some specialized functions for manipulating heterogeneous, arbitrarily-nested data structures. Changing a predicate with <code>assoc-in*</code>.</p><pre><code>(require '[speculoos.fn-in :refer
           [get-in* assoc-in* update-in* dissoc-in*]])</code><br><code>(valid-collections?
  [99 {:a 1, :b (list 42 22/7 ["abc" {:c [\z \q]}])}]
  [{:b (list [{:c [map?]}])}])
;; => false</code><br><code>(valid-collections?
  [99 {:a 1, :b (list 42 22/7 ["abc" {:c [\z \q]}])}]
  (assoc-in* [{:b (list [{:c [map?]}])}]
             [0 :b 0 0 :c 0]
             vector?))
;; => true</code></pre><p>Removing a predicate with <code>dissoc-in*</code>.</p><pre><code>(valid-collections? {:a [99 (list {:b [42]})]}
                    {:coll-test map?,
                     :a [(list {:b [map?]})]})
;; => false</code><br><code>(valid-collections? {:a [99 (list {:b [42]})]}
                    (dissoc-in* {:coll-test map?,
                                 :a [(list {:b [map?]})]}
                                [:a 0 0 :b 0]))
;; => true</code></pre><h3>Repairing data</h3><p>Adjust or remove datums such that the data collection satisfies your specification. Using <code>clojure.core</code> functions.</p><pre><code>(valid-collections? [[1] [2 3] [4 5 6]]
                    [[vector?] [vector?] [map?]])
;; => false</code><br><code>(valid-collections?
  (update-in [[1] [2 3] [4 5 6]] [2] #(zipmap [:a :b :c] %))
  [[vector?] [vector?] [map?]])
;; => true</code><br><code>(valid-collections? {:a 42, :b {:c 99}}
                    {:b {:has-anything? #(= 0 (count %))}})
;; => false</code><br><code>(valid-collections? (assoc-in {:a 42, :b {:c 99}} [:b] {})
                    {:b {:has-anything? #(= 0 (count %))}})
;; => true</code></pre><p>Using Speculoos' <code>fn-in*</code> functions.</p><pre><code>(valid-collections? [11 [22 33 [44 55 66 [77]]]]
                    [[[[list?]]]])
;; => false</code><br><code>(valid-collections? (update-in* [11 [22 33 [44 55 66 [77]]]]
                                [1 2 3]
                                #(into '() %))
                    [[[[list?]]]])
;; => true</code><br><code>(valid-collections? {:a 42, :b {:c {:d [99 0 'foo]}}}
                    {:b {:c {:d [empty?]}}})
;; => false</code><br><code>(valid-collections? (dissoc-in* {:a 42, :b {:c {:d [99]}}}
                                [:b :c :d 0])
                    {:b {:c {:d [empty?]}}})
;; => true</code></pre></section><section id="comprehensive-validation"><h2>Comprehensive validation</h2><p>Perform independent scalar and collection validations with a single function call. <code>valid?</code> returns <code>true/false</code>.</p><pre><code>(require '[speculoos.core :refer [valid? validate]])</code><br><code>(def no-strings? #(every? (complement string?) %))</code><br><code>(valid? [42 :foo \z] [int? keyword? char?] [no-strings?])
;; => true</code><br><code>(valid? {:a 42, :b ['foo "abc"]}
        {:a int?, :b [symbol? string?]}
        {:has? #(contains? % :a), :b [#(<= 2 (count %))]})
;; => true</code></pre><p><code>validate</code> returns the validation report.</p><pre><code>(validate [42 :foo \z] [int? keyword? char?] [no-strings?])
;; => ({:datum 42,
;;      :path [0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:datum :foo,
;;      :path [1],
;;      :predicate #function [clojure.core/keyword?],
;;      :valid? true}
;;     {:datum \z,
;;      :path [2],
;;      :predicate #function [clojure.core/char?--5473],
;;      :valid? true}
;;     {:datum [42 :foo \z],
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? true,
;;      :value #function [recipes/no-strings?]})</code></pre><p>Highlight invalid results with <code>only-invalid</code>.</p><pre><code>(def map-predicate {:a int?, :b string?})</code><br><code>(def no-chars? #(every? (complement char?) (vals %)))</code><br><code>(only-invalid (validate [{:a 42, :b "abc"} {:a 99, :b \z}]
                        [map-predicate map-predicate]
                        [vector? {}
                         {:any-chars? no-chars?}]))
;; => ({:datum \z,
;;      :path [1 :b],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? false}
;;     {:datum {:a 99, :b \z},
;;      :ordinal-parent-path [1],
;;      :path [2 :any-chars?],
;;      :valid? false,
;;      :value #function [recipes/no-chars?]})</code></pre><p>Require that every scalar and every collection have at least one predicate, and all those predicates are satisfied.</p><pre><code>(require '[speculoos.utility :refer [thoroughly-valid?]])</code><br><code>;; all scalars and collections have a predicate, all predicates satisfied</code><code>(thoroughly-valid? [42 {:x 'foo, :y 22/7}]
                   [int? {:x symbol?, :y ratio?}]
                   [vector? {:what-am-i? map?}])
;; => true</code><br><code>;; all scalars and collections have a predicate, ratio predicate not satisfied</code><code>(thoroughly-valid? [42 {:x 'foo, :y 99}]
                   [int? {:x symbol?, :y ratio?}]
                   [vector? {:what-am-i? map?}])
;; => false</code><br><code>;; all predicates satisfied, but nested map lacks a predicate</code><code>(thoroughly-valid? [42 {:x 'foo, :y 22/7}]
                   [int? {:x symbol?, :y ratio?}]
                   [vector? {}])
;; => false</code></pre></section><section id="function-validation"><h2>Function validation</h2><p>Check supplied arguments and/or the function's return value.</p><h3>Validating functions with external metadata</h3><p><em>ad hoc</em> function specification by wrapping function with metadata.</p><pre><code>(require '[speculoos.function-specs :refer
           [validate-fn-with validate-fn-meta-spec
            inject-specs! validate-higher-order-fn
            exercise-fn instrument unstrument]])</code><br><code>(validate-fn-with +
                  {:speculoos/arg-scalar-spec [int? int?]}
                  1
                  2)
;; => 3</code><br><code>(validate-fn-with +
                  {:speculoos/arg-scalar-spec [int? int?]}
                  1
                  9.87)
;; => ({:datum 9.87,
;;      :fn-spec-type :speculoos/argument,
;;      :path [1],
;;      :predicate #function [clojure.core/int?],
;;      :valid? false})</code></pre><h3>Injecting metadata</h3><p>Altering the function <code>var</code>'s metadata to provide the specification.</p><pre><code>(inject-specs! + {:speculoos/arg-scalar-spec [int? int?]})
;; => nil</code><br><code>(validate-fn-meta-spec + 1 2) ;; => 3</code><br><code>(validate-fn-meta-spec + 1 9.87)
;; => ({:datum 9.87,
;;      :fn-spec-type :speculoos/argument,
;;      :path [1],
;;      :predicate #function [clojure.core/int?],
;;      :valid? false})</code></pre><p>Metadata specifications are consulted only when Speculoos explicitly validates.</p><pre><code>(+ 1 9.87) ;; => 10.87</code></pre><p>Argument sequence validated as a collection.</p><pre><code>(def all-numbers? #(every? number? %))</code><br><code>(validate-fn-with +
                  {:speculoos/arg-collection-spec
                     [all-numbers?]}
                  1
                  9.87
                  22/7)
;; => 14.012857142857142</code><br><code>(validate-fn-with +
                  {:speculoos/arg-collection-spec
                     [all-numbers?]}
                  1
                  9.87
                  "abc")
;; => ({:datum [1 9.87 "abc"],
;;      :fn-spec-type :speculoos/argument,
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? false,
;;      :value #function [recipes/all-numbers?]})</code></pre><p>Performing both scalar and collection argument validation.</p><pre><code>(def length-at-most-2? #(>= 2 (count %)))</code><code>(validate-fn-with +
                  {:speculoos/arg-scalar-spec [int? int?],
                   :speculoos/arg-collection-spec
                     [length-at-most-2?]}
                  1
                  2)
;; => 3</code><br><code>(validate-fn-with
  +
  {:speculoos/arg-scalar-spec [int? int? int?],
   :speculoos/arg-collection-spec [length-at-most-2?]}
  1
  2
  3.45)
;; => ({:datum 3.45,
;;      :fn-spec-type :speculoos/argument,
;;      :path [2],
;;      :predicate #function [clojure.core/int?],
;;      :valid? false}
;;     {:datum [1 2 3.45],
;;      :fn-spec-type :speculoos/argument,
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? false,
;;      :value #function [recipes/length-at-most-2?]})</code></pre><h3>Validating functions with internal metadata</h3><p>Supply specifications during function definition.</p><pre><code>(defn add-two-things
  {:speculoos/arg-scalar-spec [float? ratio?]}
  [x y]
  (+ x y))</code><br><code>(add-two-things 1.23 22/7) ;; => 4.372857142857143</code><code>(add-two-things 1 2) ;; => 3</code><br><code>(validate-fn-meta-spec add-two-things 1.23 22/7)
;; => 4.372857142857143</code><br><code>(validate-fn-meta-spec add-two-things 1 2)
;; => ({:datum 1,
;;      :fn-spec-type :speculoos/argument,
;;      :path [0],
;;      :predicate #function [clojure.core/float?],
;;      :valid? false}
;;     {:datum 2,
;;      :fn-spec-type :speculoos/argument,
;;      :path [1],
;;      :predicate #function [clojure.core/ratio?],
;;      :valid? false})</code></pre><h3>Validating higher-order functions</h3><p>Supply enough arguments so that the <span class="small-caps">hof</span> returns a non-function.</p><pre><code>(defn example-hof
  [a b]
  (fn [c d] (fn [e f] (+ (* a c e) (* b d f)))))</code><br><code>(((example-hof 20 2) 30 3) 40 4) ;; => 24024</code><br><code>(inject-specs! example-hof
               {:speculoos/arg-scalar-spec [int? int?],
                :speculoos/hof-specs
                  {:speculoos/arg-scalar-spec [int? int?],
                   :speculoos/hof-specs
                     {:speculoos/arg-scalar-spec [int?
                                                  int?]}}})
;; => nil</code><br><code>(validate-higher-order-fn example-hof [20 2] [30 3] [40 4])
;; => 24024</code><br><code>(validate-higher-order-fn example-hof
                          [20 2.0]
                          [30 3.0]
                          [40 4.0])
;; => ({:datum 2.0,
;;      :fn-tier :speculoos/argument,
;;      :path [0 1],
;;      :predicate #function [clojure.core/int?],
;;      :valid? false}
;;     {:datum 3.0,
;;      :fn-tier :speculoos/argument,
;;      :path [1 1],
;;      :predicate #function [clojure.core/int?],
;;      :valid? false}
;;     {:datum 4.0,
;;      :fn-tier :speculoos/argument,
;;      :path [2 1],
;;      :predicate #function [clojure.core/int?],
;;      :valid? false})</code></pre><h3>Instrumenting functions</h3><p>Arguments and returns implicitly validated on every function invocation.</p><pre><code>(defn multiplier
  {:speculoos/arg-scalar-spec [int? pos-int? ratio?],
   :speculoos/arg-return-spec [number? string?]}
  [x y z]
  [(* x y z) (str y)])</code><br><code>(multiplier 2 3 8/2) ;; => [24 "3"]</code><code>(multiplier 2 3 4) ;; => [24 "3"]</code><br><code>(instrument multiplier)</code><code>(multiplier 2 3 8/2) ;; => [24 "3"]</code><code>(multiplier 2 3 4) ;; => [24 "3"]</code><br><code>(with-out-str (multiplier 2 3 4))
;; => ({:path [2], :datum 4, :predicate #function[clojure.core/ratio?], :valid? false})
</code><br><code>(unstrument multiplier) ;; => {}</code><code>(multiplier 2 3 8/2) ;; => [24 "3"]</code><code>(multiplier 2 3 4) ;; => [24 "3"]</code></pre><h3>Exercising functions</h3><p>Invoke function repeatedly with sample data generated from argument scalar specification.</p><pre><code>(defn shoe
  {:speculoos/arg-scalar-spec [pos-int? pos-int? string?]}
  [x y z]
  (str x ", " y ", buckle my " z))</code><br><code>(shoe 3 4 "sandal") ;; => "3, 4, buckle my sandal"</code><br><code>(exercise-fn shoe 3)
;; => ([[6 28 "8iZo23Ix2vxQT"] "6, 28, buckle my 8iZo23Ix2vxQT"]
;;     [[12 25 "oxy7Bg4k7Vo9J9UpN1qxD3tzZg"]
;;      "12, 25, buckle my oxy7Bg4k7Vo9J9UpN1qxD3tzZg"]
;;     [[12 12 "yGtk"] "12, 12, buckle my yGtk"])</code></pre><h3>Validating macros</h3><p>Speculoos validates macros by applying specifications against the macro expansion.</p><pre><code>(defmacro infix
  [form]
  (list (second form) (first form) (last form)))</code><br><code>(infix (2 + 3)) ;; => 5</code><code>(macroexpand-1 '(infix (2 + 3))) ;; => (+ 2 3)</code><br><code>(def infix-spec (list symbol? int? int?))</code><br><code>(require '[speculoos.core :refer [valid-macro?]])</code><br><code>(valid-macro? '(infix (2 + 3)) infix-spec) ;; => true</code><br><code>(valid-macro? '(infix (2.2 + 3.3)) infix-spec) ;; => false</code></pre></section><section id="fn-in"><h2><code>fn*</code> and <code>fn-in*</code></h2><p>Starred functions inspect and manipulate values buried in heterogeneous, arbitrarily-nested data structures.</p><h3>Inspecting values</h3><pre><code>(require '[speculoos.fn-in :refer
           [get-in* assoc-in* update-in* dissoc-in*]])</code><br><code>(get-in* [11 22 [33 44 55 [66 [77 [88 99]]]]] [2 3 1 1 1])
;; => 99</code><br><code>(get-in* {:a {:b {:c {:d 99}}}} [:a :b :c :d]) ;; => 99</code><br><code>(get-in* (list 11 22 33 (list 44 (list 55))) [3 1 0])
;; => 55</code><br><code>(get-in* #{11 #{22}} [#{22} 22]) ;; => 22</code><br><code>(get-in* [11 22 {:a 33, :b [44 55 66 {:c [77 88 99]}]}]
         [2 :b 3 :c 2])
;; => 99</code><br><code>(get-in* {:a (list {} {:b [11 #{33}]})} [:a 1 :b 1 33])
;; => 33</code></pre><h3>Associating values</h3><pre><code>(assoc-in* [11 [22 [33 [44 55 66]]]] [1 1 1 2] :new-val)
;; => [11 [22 [33 [44 55 :new-val]]]]</code><br><code>(assoc-in* {:a {:b {:c 42}}} [:a :b :c] 99)
;; => {:a {:b {:c 99}}}</code><br><code>(assoc-in* {:a [11 22 33 [44 55 {:b [66 {:c {:d 77}}]}]]}
           [:a 3 2 :b 1 :c :d]
           "foo")
;; => {:a [11 22 33 [44 55 {:b [66 {:c {:d "foo"}}]}]]}</code></pre><h3>Updating values</h3><pre><code>(update-in* [11 22 33 [44 [55 66 [77 88 99]]]]
            [3 1 2 2]
            inc)
;; => [11 22 33 [44 [55 66 [77 88 100]]]]</code><br><code>(update-in* {:a [11 22 {:b 33, :c [44 55 66 77]}]}
            [:a 2 :c 1]
            #(+ 5500 %))
;; => {:a [11 22 {:b 33, :c [44 5555 66 77]}]}</code></pre><h3>Dissociating values</h3><pre><code>(dissoc-in* [11 22 [33 [44 55 66]]] [2 1 1])
;; => [11 22 [33 [44 66]]]</code><br><code>(dissoc-in* {:a [11 22 33 {:b 44, :c [55 66 77]}]}
            [:a 3 :c 0])
;; => {:a [11 22 33 {:b 44, :c [66 77]}]}</code></pre><h3>Clamping non-terminating sequences</h3><pre><code>(require '[speculoos.utility :refer [clamp-in*]])</code><br><code>(clamp-in* [11 22 33 [44 55 [66 (repeat 42)]]] [3 2 1] 3)
;; => [11 22 33 [44 55 [66 [42 42 42]]]]</code><br><code>(clamp-in* {:a [11 22 33 {:b (cycle ['foo "bar" \z])}]}
           [:a 3 :b]
           5)
;; => {:a [11 22 33 {:b [foo "bar" \z foo "bar"]}]}</code></pre></section><section id="utility"><h2>Utility functions</h2><p>Functions to get things done with Speculoos.</p><h3>All paths</h3><p>Exhaustive sequence of paths of all values in a heterogeneous, arbitrarily-nested data structure.</p><pre><code>(speculoos.core/all-paths [11 22 {:a 'foo, :b \z}])
;; => [{:path [], :value [11 22 {:a foo, :b \z}]}
;;     {:path [0], :value 11} {:path [1], :value 22}
;;     {:path [2], :value {:a foo, :b \z}}
;;     {:path [2 :a], :value foo} {:path [2 :b], :value \z}]</code></pre><h3>Data from scalar specification</h3><pre><code>(require '[speculoos.utility :refer
           [data-from-spec spec-from-data
            basic-collection-spec-from-data all-specs-okay
            scalars-without-predicates
            collections-without-predicates exercise in?
            defpred unfindable-generators
            validate-predicate->generator]])</code><br><code>(data-from-spec [int? {:a string?, :b ratio?}])
;; => [42 {:a "abc", :b 22/7}]</code></pre><h3>Scalar specification from data</h3><pre><code>(spec-from-data {:a 42, :b [\z 'foo "xyz"], :c 2/3})
;; => {:a #function [clojure.core/int?],
;;     :b [#function [clojure.core/char?--5473]
;;         #function [clojure.core/symbol?]
;;         #function [clojure.core/string?--5475]],
;;     :c #function [clojure.core/ratio?]}</code></pre><p>Collection specification from data</p><pre><code>(basic-collection-spec-from-data
  ['ignored-val {:ignored-key 'another-ignored-val} (list)])
;; => [{:collection-predicate map?} (list?) vector?]</code></pre><h3>Checking specifications</h3><pre><code>(all-specs-okay [int? string? {:a char?}]) ;; => true</code><br><code>(all-specs-okay [int? :not-a-predicate {:a char?}])
;; => ({:path [1], :value :not-a-predicate})</code></pre><h3>Data without a corresponding scalar predicate</h3><pre><code>(scalars-without-predicates [11 {:a 22, :b "abc"}]
                            [int? {:b string?}])
;; => ({:path [1 :a], :value 22})</code></pre><h3>Collections without a corresponding collection predicate</h3><pre><code>(collections-without-predicates
  [42 {:a (list 22/7)} ['foo]]
  [vector? {:a (list list?), :is-map? map?}])
;; => #{{:path [2], :value [foo]}}</code></pre><h3>Filtering validation results</h3><pre><code>(only-invalid (validate-scalars [11 ["abc" [:foo]]]
                                [int? [string? [symbol?]]]))
;; => ({:datum :foo,
;;      :path [1 1 0],
;;      :predicate #function [clojure.core/symbol?],
;;      :valid? false})</code></pre><h3>Exercising specifications</h3><pre><code>(exercise {:a int?, :b [char? keyword?]} 3)
;; => ([{:a 364, :b [\2 :SZ]} true]
;;     [{:a 556, :b [\g :BR]} true]
;;     [{:a 384, :b [\o :?sS6Y3]} true])</code></pre><h3>Advanced random sample generators</h3><p>Built-in basic generator, set generator, and string regex generator.</p><pre><code>(data-from-spec [int? #{:water :coffee :tea :milk}
                 #"fo{3,5}!"]
                :random)
;; => [-409 :tea "fooooo!"]</code></pre><p>Custom generators attached to predicate's metadata.</p><pre><code>(def manual-predicate
  (with-meta #(int? %)
    {:speculoos/predicate->generator #(rand-int 99)}))</code><br><code>(defpred easier-predicate #(int? %) #(rand-int 99))</code><br><code>(data-from-spec [manual-predicate easier-predicate] :random)
;; => [6 57]</code></pre><p>Automatic random sample generators. <code>or</code> generators emit multiple types.</p><pre><code>(defpred or-predicate
         #(or (int? %) (string? %) (keyword? %)))</code><br><code>(data-from-spec (repeat 5 or-predicate) :random)
;; => ["117sVCJ2Ams660hcBYioNN" :! :?Y.9s7S 24 29]</code></pre><p><code>and</code> refines a basic generator.</p><pre><code>(defpred and-predicate
         (fn [i] (and (int? i) (odd? i) (<= 3 i))))</code><br><code>(data-from-spec (repeat 5 and-predicate) :random)
;; => [29 29 9 27 27]</code></pre><p>Combining <code>and</code>/<code>or</code>.</p><pre><code>(defpred and-or-predicate
         #(or (and (int? %) (pos? %))
              (and (string? %) (<= 3 (count %)))
              (and (ratio? %) (<= 1/9 %))))</code><br><code>(data-from-spec (repeat 4 and-or-predicate) :random)
;; => [7/2 23/25 "1eSSSlKXPMpN6o2Y4Z22yn0JDk" "baiPcIqp65QS4"]</code></pre><p>Identifying predicates without generators.</p><pre><code>(unfindable-generators
  [manual-predicate (fn no-gen [] (rand-int 99)) easier-predicate])
;; => [{:path [1], :value no-gen}]</code></pre><p>Checking a generator against its host predicate.</p><pre><code>(defpred pred-with-broken-gen #(neg? %) #(rand-int 99))</code><br><code>(validate-predicate->generator pred-with-broken-gen 5)
;; => ([14 false] [46 false] [57 false] [40 false] [12 false])</code></pre><h3>Validate with path specification</h3><p>Alternative validation function that uses explicit paths to any value — scalar or collection — and multi-arity predicates.</p><pre><code>(def path-predicate #(= (/ %1 %2) %3))</code><br><code>(speculoos.core/validate-with-path-spec
  [11 {:b [\z "abc" 22]} 2]
  [{:paths [[1 :b 2] [0] [2]], :predicate path-predicate}])
;; => ({:args (22 11 2),
;;      :paths [[1 :b 2] [0] [2]],
;;      :predicate #function [recipes/path-predicate],
;;      :valid? true})</code></pre><h3>Item in collection?</h3><p>Alternative to <code>clojure.core/contains?</code>.</p><pre><code>;; probably not what you expected</code><code>(contains? [:a :b :c] 1) ;; => true</code><br><code>;; use `in?` instead</code><code>(in? [:a :b :c] 1) ;; => false</code><code>(in? [:a :b :c] :b) ;; => true</code><code>(in? (list 'foo 'bar) 'bar) ;; => true</code></pre></section><section id="predicates"><h2>Predicates</h2><p>Speculoos predicates are regular Clojure functions and compose the same way.</p><pre><code>(valid-scalars? [42 "abc"]
                [#(and (int? %) (even? %))
                 #(and (string? %) (= 3 (count %)))])
;; => true</code><br><code>(valid-scalars? {:a 'foo} {:a #(or (char? %) (symbol? %))})
;; => true</code></pre><p><code>clojure.core</code> provides some nice higher-order functions for composing predicates: <code>every-pred</code> and <code>some-fn</code>.</p><pre><code>(valid-scalars? [42 "abc"]
                [(every-pred int? even?)
                 (some-fn symbol? string?)])
;; => true</code></pre><h3>Placeholders, ambivalence</h3><p>Maintain a specification's correspondence to a sequence.</p><pre><code>(valid-scalars? [42 "abc" :foo] [int? symbol? keyword?])
;; => false</code><br><code>(valid-scalars? [42 "abc" :foo] [int? any? keyword?])
;; => true</code><br><code>(valid-scalars? [42 "abc" :foo]
                [int? (constantly true) keyword?])
;; => true</code><br><code>(valid-scalars? [42 "abc" :foo]
                [int? (fn [_] true) keyword?])
;; => true</code></pre><h3>Regular expression predicates</h3><p><code>nil</code> returned from a regular expression failure can indicate an invalid string.</p><pre><code>(def example-regex #"\w{3}\d{3}")</code><br><code>(re-matches example-regex "abc123") ;; => "abc123"</code><code>(re-matches example-regex "qr89") ;; => nil</code><br><code>(def regex-predicate #(re-matches example-regex %))</code><br><code>(valid-scalars? ["abc123" "xyz789"]
                [regex-predicate regex-predicate])
;; => true</code><br><code>(only-invalid (validate-scalars ["abc123" "qr89"]
                                [regex-predicate
                                 regex-predicate]))
;; => ({:datum "qr89",
;;      :path [1],
;;      :predicate #function [recipes/regex-predicate],
;;      :valid? nil})</code></pre><p>Speculoos treats free-floating regexes as predicates.</p><pre><code>(validate-scalars ["abc123" "qr89"]
                  [#"\w{3}\d{3}" #"[qrs]{2}[789]{2}"])
;; => [{:datum "abc123",
;;      :path [0],
;;      :predicate #"\w{3}\d{3}",
;;      :valid? "abc123"}
;;     {:datum "qr89",
;;      :path [1],
;;      :predicate #"[qrs]{2}[789]{2}",
;;      :valid? "qr89"}]</code></pre><h3>Sequence regexes</h3><p>Mimicking <code>spec.alpha</code>'s sequence regexes, use <code>speculoos.utility/seq-regex</code> during a collection validation.</p><pre><code>(def three-ints-two-kw-and-syms?
  #(seq-regex % int? 3 keyword? 2 symbol? :*))</code><br><code>(valid-collections? [1 2 3 :a :b 'foo 'bar 'baz 'qux]
                    [three-ints-two-kw-and-syms?])
;; => true</code><br><code>(def sym-str-opt-int?
  #(seq-regex % symbol? :. string? :. int? :?))</code><br><code>(valid-collections? (list 'foo "abc" 42)
                    (list sym-str-opt-int?))
;; => true</code></pre><h3>Sets as predicates</h3><p>Within a scalar specification, a set creates a predicate that tests for membership.</p><pre><code>(valid-scalars? [:orange] [#{:red :orange :yellow}])
;; => true</code><br><code>(valid-scalars? [:pantheon] [#{:red :orange :yellow}])
;; => false</code></pre><h3>Testing functions as datums</h3><p>When the data contains a function, predicates can perform <em>property testing</em> to validate the datum.</p><pre><code>(defn one-something-two-equals-three? [f] (= 3 (f 1 2)))</code><br><code>(one-something-two-equals-three? +) ;; => true</code><br><code>(validate-scalars [+ -]
                  [one-something-two-equals-three?
                   one-something-two-equals-three?])
;; => [{:datum #function [clojure.core/+],
;;      :path [0],
;;      :predicate #function
;;                  [recipes/one-something-two-equals-three?],
;;      :valid? true}
;;     {:datum #function [clojure.core/-],
;;      :path [1],
;;      :predicate #function
;;                  [recipes/one-something-two-equals-three?],
;;      :valid? false}]</code></pre></section></article><p id="page-footer">Copyright © 2024 Brad Losavio.<br>Compiled 2024 July 24.<span id="uuid"><br>256d193f-cd45-4a67-bf67-abbe6eb1776c</span></p></body></head></html>