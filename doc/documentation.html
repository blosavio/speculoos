<!DOCTYPE html>
<html lang="en"><head><link href="tufte.css" rel="stylesheet" type="text/css"><link href="speculoos.css" rel="stylesheet" type="text/css"><title>Speculoos --- Documentation</title><meta charset="utf-8" compile-date="2024-07-24 09:05:18" content="width=device-width, initial-scale=1" name="viewport"><body><nav id="nav-bar"><ul><li><a href="home.html">Home</a></li><li><a href="ideas.html">Ideas</a></li><li>Documentation</li><li><a href="recipes.html">Recipes</a></li><li><a href="diff.html"><code>diff</code></a></li><li><a href="pros_cons.html">Pros, Cons, &amp; Alts</a></li><li class="small-caps"><a href="index.html">api</a></li><li><a href="source.html">Source</a></li><li><a href="contact.html">Contact</a></li></ul></nav><article><h1>Speculoos Documentation</h1><section class="nav-section"><p><a href="#description">Description</a><br><a href="#implementation">Implementation</a><br><a href="#principles">Speculoos working principles</a><br><a href="#core">Core functions</a><br><a href="#fn-in*"><code>fn-in*</code></a><br><a href="#predicates">Predicates</a><br><a href="#utility">Utility functions</a><br><a href="#fn-specs">Functions specification and testing</a><br><a href="#non-terminating">Non-terminating sequences</a><br><a href="#sets">Sets</a><br><a href="#path-spec">Un digestif</a><br><a href="#glossary">Glossary</a><br></p></section><section id="description"><h2>Description</h2><p>The Speculoos library validates Clojure data. Building on that capability, Speculoos can test a function's arguments, its return values, and the relationships between them. Speculoos can also generate sample data that may be used to test functions. In short, Speculoos attempts to do what <code>clojure.spec.alpha</code> does, while exploring <a href="ideas.html">three ideas</a>.</p><p>Let's consider validation. <em>Does our data look the way we expect?</em> Validation is the process of creating a specification, a human- and machine-readable declaration about properties of data, and then checking if an instance of data satisfies that specification.</p><p>Pretend Dominique's service accepts a string. Clojure's <code>clojure.core/string?</code> tests whether something is a string.</p><pre><code>(string? "big parade") ;; => true</code><code>(string? 76) ;; => false</code></pre><p>We now know that we can send Dominique's service <code>"big parade"</code> but not <code>76</code>.</p><p>Dominique updates her service, and now she requires a <em>vector which contains a string.</em> We could write a bespoke function to validate this new requirement, but Dominique's service will probably evolve, and so we decide to pull in Speculoos as a dependency to handle more sophisticated validation.</p><pre><code>(require '[speculoos.core :refer
           [valid-scalars? validate-scalars only-invalid]])</code></pre><p>So we stuff <code>string?</code> into a vector and ask Speculoos to apply it to our data.</p><pre><code>(valid-scalars? ["big parade"] [string?]) ;; => true</code></pre><p>Speculoos' <code>valid…?</code> family of functions take data as the first argument and our specification as the second argument.</p><p>Dominique later tells us that she needs more information, and her requirement expands to <em>vector which contains a string, followed by a keyword and an integer</em>. We create a Speculoos specification by arranging predicates in the same pattern as we expect the elements to appear in the data.</p><pre><code>(valid-scalars? ["big parade" :trombones 76]
                [string? keyword? int?])
;; => true</code></pre><p>Speculoos marches along our vector, testing each element against the predicate in our specification. In this example, Speculoos tells us that <code>["big parade" :trombones 67]</code> satisfies all our properties and returns <code>true</code>. What if we supply a non-sensical number of trombones?</p><pre><code>(valid-scalars? ["big parade" :trombones \z]
                [string? keyword? int?])
;; => false</code></pre><p>Sure enough, the character <code>\z</code> is not a valid number of trombones in a big parade. We've specified our data in a way that humans and machines can understand and validated some data.</p><p>Ever busy, Dominique tells us we now need to validate any number of suffixed keyword+integer pairs. So we <em>compose</em> a new specification. <code>concat</code> combines two specification sub-components while <code>cycle</code> gives us an infinite sequence of keyword/integer pairs.</p><pre><code>(def our-composed-spec
  (concat [string?] (cycle [keyword? int?])))</code></pre><p>Let's see what happens when we toss Speculoos some larger data and that non-terminating specification.</p><pre><code>(valid-scalars? ["parade" :trombones 76 :cornets 110
                 :bassoons 0]
                our-composed-spec)
;; => true</code></pre><p>Speculoos only validates elements that are contained both in the data and in the specification, so the fact that <code>cycle</code> never terminates makes it a powerful tool to specify an arbitrarily long sequence.</p><p>Our friend Ahmed's service also requires data of a particular shape, but in his case, a map that looks something like <code>{:species "oak" :type :deciduous}</code>. Speculoos validates maps by applying predicates found in the identical keys of the specification.</p><pre><code>(valid-scalars? {:species "oak", :type :deciduous}
                {:species string?, :type keyword?})
;; => true</code></pre><p>And if we supply data with the wrong type, for example, a string instead of a keyword</p><pre><code>(valid-scalars? {:species "oak", :type "deciduous"}
                {:species string?, :type keyword?})
;; => false</code></pre><p>Speculoos reports that the data is not valid.</p><p>Ahmed tells us that the <code>:type</code> may only be either <code>:deciduous</code> or <code>:coniferous</code>. Clojure sets make wonderful predicates</p><pre><code>(#{:coniferous :deciduous} :deciduous) ;; => :deciduous</code><code>(#{:coniferous :deciduous} :shrubbery) ;; => nil</code></pre><p> so we tighten our specification to test membership in Ahmed's enumerated set.</p><pre><code>(valid-scalars? {:species "oak", :type :deciduous}
                {:species string?,
                 :type #{:deciduous :coniferous}})
;; => true</code><br><code>(valid-scalars? {:species "oak", :type :confetti}
                {:species string?,
                 :type #{:deciduous :coniferous}})
;; => false</code></pre><p>Speculoos tells us that <code>:confetti</code> is not one of the expected values.</p><p>Gertrude challenges us with a new task: validate some elements in a nested data structure. She supplies us with some data.</p><pre><code>(def hobbit
  {:first-name "Peregrin",
   :last-name "Took",
   :nick-name "Pippin",
   :home-town "The Shire",
   :friends ["Frodo" "Samwise" "Merry" "Aragorn" "Legolas"
             "Gmili" :Gandalf]})</code></pre><p>We then write a specification for single hobbit.</p><pre><code>(def hobbit-spec
  {:first-name string?,
   :last-name string?,
   :favorite-ice-cream keyword?,
   :friends (repeat string?)})</code></pre><p>Remembering that Speculoos only validates elements in both the data and the specification, we take advantage of <code>repeat</code> to supply an arbitrarily long sequence of hobbit friend specifications.</p><pre><code>(valid-scalars?
  [{:first-name "Meriadoc"} {:last-name :Brandybuck}
   {:favorite-pipe-weed "Longbottom Leaf"} hobbit]
  (repeat hobbit-spec))
;; => false</code></pre><p>In this example, we can see the problems by eye: the hobbit's <code>:last-name</code> and Pippin's final friend, Gandalf, should both be strings, but in this instance both are invalid keywords. Speculoos' family of <code>valid…?</code> functions merely return <code>true/false</code>. Speculoos provides some helper functions that highlight the interesting, invalid elements.</p><pre><code>(only-invalid
  (validate-scalars
    [{:first-name "Meriadoc"} {:last-name :Brandybuck}
     {:favorite-pipe-weed "Longbottom Leaf"} hobbit]
    (repeat hobbit-spec)))
;; => ({:datum :Brandybuck,
;;      :path [1 :last-name],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? false}
;;     {:datum :Gandalf,
;;      :path [3 :friends 6],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? false})</code></pre><p>Now we can tell Gertrude precisely where the problems are. The <em>path</em> elements of the report provide an unambiguous address to the invalid data elements. The path <code>[1 :last-name]</code> can be used to inspect the data like this.</p><pre><code>(require '[speculoos.fn-in :refer
           [get-in* assoc-in* update-in* dissoc-in*]])</code><br><code>(get-in* [{:first-name "Meriadoc"} {:last-name :Brandybuck}
          {:favorite-pipe-weed "Longbottom Leaf"} hobbit]
         [1 :last-name])
;; => :Brandybuck</code></pre><p><code>get-in*</code> extracts the map at the outer vector's <code>1</code> index, then gets the nested map's value keyed by <code>:last-name</code>, which in this case is <code>:Brandybuck</code>.</p><p>Speculoos provides analogous functions for altering or removing values in a nested structure. We could 'repair' the hobbity data to a valid state like this</p><pre><code>(def repaired-Pippin
  (assoc-in* hobbit [:friends 6] "Gandalf"))</code></pre><p>such that we can send Gertrude a proper hobbit.</p><pre><code>(valid-scalars? repaired-Pippin hobbit-spec) ;; => true</code></pre><p>Viraj has made a machine that requires a vector which contains at least five elements. So far, we have been using Speculoos to validate <em>scalars</em>, indivisible elements such as strings, keywords, and integers. The vector's length is a property of the <em>collection</em>, and Speculoos validates collection specifications in a distinct process. First, we define a collection specification with a useful name.</p><pre><code>(def length-six-or-greater? #(<= 6 (count %)))</code></pre><p>Then we can validate some sample data to see if it satisfies the collection specification.</p><pre><code>(require '[speculoos.core :refer
           [valid-collections? validate-collections]])</code><br><code>(valid-collections? [:Dorothy :Toto :Scarecrow :Tin-Man
                     :Cowardly-Lion :Wizard-of-Oz]
                    [length-six-or-greater?])
;; => true</code></pre><p>Let's see what happens if we don't supply Viraj with enough data.</p><pre><code>(valid-collections? [:water :coffee :tea]
                    [length-six-or-greater?])
;; => false</code></pre><p>Viraj will be disappointed. Speculoos can tell us more than <code>true/false</code> if we invoke a different function</p><pre><code>(validate-collections [:water :coffee :tea]
                      [length-six-or-greater?])
;; => ({:datum [:water :coffee :tea],
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? false,
;;      :value #function [documentation/length-six-or-greater?]})</code></pre><p>One of Speculoos' core ideas is that collections ought to be validated separately from the scalars they contain. However, Viraj wants to specify two concepts: his vector must contain at least six elements, and each of those elements must be a keyword or a string. We've already written his collection specification: the vector must contain at least six elements. We can write his scalar specification like this.</p><pre><code>(def kw-or-string? #(or (keyword %) (string? %)))</code></pre><p>We give that scalar specification a couple of quick tests.</p><pre><code>(valid-scalars? [:Dorothy :Toto "Scarecrow" :Tin-Man
                 "Cowardly-Lion" :Wizard-of-Oz]
                (repeat kw-or-string?))
;; => true</code><br><code>(valid-scalars? [:Dorothy 42 :Toto "Scarecrow" :Tin-Man
                 "Cowardly-Lion" :Wizard-of-Oz]
                (repeat kw-or-string?))
;; => false</code></pre><p>Though scalars and collections are specified separately, for any one chunk of data, we'll often want to specify aspects of its scalars and aspects of their containing collections. Speculoos provides functions to validate both simultaneously, but distinctly. We invoke <code>valid?</code> with the data as the first argument, the scalar specification as the second argument, and the collection specification as the third argument.</p><pre><code>(require '[speculoos.core :refer [valid? validate]])</code><br><code>(valid? [:Dorothy :Toto "Scarecrow" :Tin-Man "Cowardly-Lion"
         :Wizard-of-Oz]
        (repeat kw-or-string?)
        [length-six-or-greater?])
;; => true</code></pre><p>Speculoos can inform Viraj with details on invalid properties of his data.</p><pre><code>(only-invalid (validate [:Dorothy 42 :Toto "Scarecrow"]
                        (repeat kw-or-string?)
                        [length-six-or-greater?]))
;; => ({:datum 42,
;;      :path [1],
;;      :predicate #function [documentation/kw-or-string?],
;;      :valid? false}
;;     {:datum [:Dorothy 42 :Toto "Scarecrow"],
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? false,
;;      :value #function [documentation/length-six-or-greater?]})</code></pre><p>Yup, <code>42</code> is neither a keyword nor a string, and the vector itself is not long enough. We validated the scalars and collections separately, but with a single function invocation.</p><p>Now that we know how to use Speculoos to specify and validate Clojure data structures, we can explore how to specify and test the behavior of Clojure functions. A function's argument list is merely a sequence of values, and a function's return is also a value, possibly a heterogeneous, arbitrarily-nested data structure. Let's imagine our friend Himari has asked us to work on this function.</p><pre><code>(defn dental-recommendation
  "101% statistically-rigorous hygiene advice given integer n and flavor."
  {:speculoos/arg-scalar-spec [pos-int? string?],
   :speculoos/ret-scalar-spec string?}
  [n flavor]
  (str n
       " out of "
       (inc n)
       " dentists recommend "
       flavor
       "-o-Dent toothpaste!"))</code><br><code>(dental-recommendation 4 "Mint")
;; => "4 out of 5 dentists recommend Mint-o-Dent toothpaste!"</code></pre><p>Note the specifications are included in the function's metadata. Since Speculoos specifications are human- and machine-readable, they serve both as documentation,<label class="margin-toggle sidenote-number" for="doc-enhancement"></label><input class="margin-toggle" id="doc-enhancement" type="checkbox"><span class="sidenote">Also note that the docstring misleadingly calls for an integer, while the argument specification more rigorously calls for a <em>positive</em> integer. Specifications, as in this instance, can potentially communicate more and better information to your library users.</span></input> and as a basis to perform tests and validation of the function. Speculoos will validate any present scalar specification or collection specification on the argument list or the return values. In addition, Speculoos will perform an <em>argument-vs-return</em> validation, analogous to the one performed by <code>spec.alpha</code>. Merely adding the meta-data to the function does not cause any action.</p><pre><code>(dental-recommendation 4.4 "Taco")
;; => "4.4 out of 5.4 dentists recommend Taco-o-Dent toothpaste!"</code></pre><p><code>4.4</code> is not an integer, but Speculoos was not watching. We must explicitly request that Speculoos perform a validation.</p><pre><code>(require '[speculoos.function-specs :refer
           [validate-fn-meta-spec validate-fn-with
            exercise-fn]])</code><br><code>(validate-fn-meta-spec dental-recommendation 4 :Jalapeno)
;; => ({:datum :Jalapeno,
;;      :fn-spec-type :speculoos/argument,
;;      :path [1],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? false})</code></pre><p>Oops, <code>:Jalapeno</code> is not a string as required by the argument scalar specification, so Speculoos tosses a report. Speculoos can also validate a function on-the-fly. For demonstration purposes, we'll specify a character for the second argument, but supply a string, which we have previously considered valid.</p><pre><code>(validate-fn-with dental-recommendation
                  {:speculoos/arg-scalar-spec [pos-int?
                                               char?]}
                  88
                  "Filet Mignon")
;; => ({:datum "Filet Mignon",
;;      :fn-spec-type :speculoos/argument,
;;      :path [1],
;;      :predicate #function [clojure.core/char?--5473],
;;      :valid? false})</code></pre><p>Sure enough, <code>"Filet Mignon"</code> is not a character. Finally, Speculoos can peek at the argument specification and generate test data for exercising Himari's function.</p><pre><code>(exercise-fn dental-recommendation 5)
;; => ([[12 "jlUmu"]
;;      "12 out of 13 dentists recommend jlUmu-o-Dent toothpaste!"]
;;     [[17 "3uGSQ995Yj8vW8KEzzj17davDuOyZk"]
;;      "17 out of 18 dentists recommend 3uGSQ995Yj8vW8KEzzj17davDuOyZk-o-Dent toothpaste!"]
;;     [[11 "9TA"]
;;      "11 out of 12 dentists recommend 9TA-o-Dent toothpaste!"]
;;     [[20 "k9u3"]
;;      "20 out of 21 dentists recommend k9u3-o-Dent toothpaste!"]
;;     [[5 "7i3NL0pQbPxFhzKj51yYn3P8HcD"]
;;      "5 out of 6 dentists recommend 7i3NL0pQbPxFhzKj51yYn3P8HcD-o-Dent toothpaste!"])</code></pre><p>Speculoos generated five pairs of random positive integers and random strings and invoked Himari's function.</p><p>We've now taken a whirlwind tour of how Speculoos can help specify and validate Clojure data structures, and how that can be used to describe Clojure functions, check their arguments, and automatically test their operation. Now, let's talk about Speculoos' implementation.</p></section><section id="implementation"><h2>Implementation</h2><p>Understanding a little bit about Speculoos' inner workings will help with effectively using it. Speculoos is not clever. Nor is it efficient. It's first step in handling any Clojure data structure is to exhaustively enumerate the path to every element. Let's take a look.</p><pre><code>(require '[speculoos.core :refer [all-paths]])</code><br><code>(all-paths [42 "abc" :foo])
;; => [{:path [], :value [42 "abc" :foo]} {:path [0], :value 42}
;;     {:path [1], :value "abc"} {:path [2], :value :foo}]</code></pre><p><code>all-paths</code><label class="margin-toggle sidenote-number" for="all-paths"></label><input class="margin-toggle" id="all-paths" type="checkbox"><span class="sidenote"><code>all-paths</code> might be useful to write your own utility functions.</span></input> recursively walks through any heterogeneous, arbitrarily-nested Clojure data structure and gathers a <code>:path</code> and <code>:value</code> for each element. The path for each element is a sequence of values that unambiguously addresses where in the structure to find that element. In this example, zero-based integers address elements contained in our vector. <code>42</code> resides at index zero, <code>"abc"</code> at index one, etc. Note that the outer parent vector has a path of <code>[]</code>.</p><p>Similar story for maps.</p><pre><code>(all-paths {:a 42, :b "abc", :c true})
;; => [{:path [], :value {:a 42, :b "abc", :c true}}
;;     {:path [:a], :value 42} {:path [:b], :value "abc"}
;;     {:path [:c], :value true}]</code></pre><p>Map elements are addressed by their keys. Very often, the key is a Clojure keyword, and the element's path contains that keyword. However, any Clojure value can be a map key. Integers are valid map keys.</p><pre><code>(all-paths
  {0 "zero value", 1 "one value", 99 "ninety-nine value"})
;; => [{:path [],
;;      :value
;;        {0 "zero value", 1 "one value", 99 "ninety-nine value"}}
;;     {:path [0], :value "zero value"}
;;     {:path [1], :value "one value"}
;;     {:path [99], :value "ninety-nine value"}]</code></pre><p>The paths for those elements are therefore integers. Composite values are also valid map keys.</p><pre><code>(all-paths {[1 2 3] "one-two-three val",
            {:a 1} "a-1-map val",
            [] "empty vec value"})
;; => [{:path [],
;;      :value {[1 2 3] "one-two-three val",
;;              [] "empty vec value",
;;              {:a 1} "a-1-map val"}}
;;     {:path [[1 2 3]], :value "one-two-three val"}
;;     {:path [{:a 1}], :value "a-1-map val"}
;;     {:path [[]], :value "empty vec value"}]</code></pre><p>In this case, the elements' paths are addressed by those composite keys. <code>"a-1-map"</code> is addressed by the key <code>{:a 1}</code>. The path to <code>"one-two-three val"</code> is <code>[1 2 3]</code>.</p><p>Perhaps you've noticed that I've used the term <em>heterogeneous, arbitrarily-nested data structure</em>. This path principle extends to every element type, at any nesting level. Let's look at a few illustrations. First, paths for nested elements.</p><pre><code>(all-paths [11 [22 33] 44 [[55]]])
;; => [{:path [], :value [11 [22 33] 44 [[55]]]}
;;     {:path [0], :value 11} {:path [1], :value [22 33]}
;;     {:path [1 0], :value 22} {:path [1 1], :value 33}
;;     {:path [2], :value 44} {:path [3], :value [[55]]}
;;     {:path [3 0], :value [55]} {:path [3 0 0], :value 55}]</code></pre><p>Elements in the top-level vector, in this example <code>11</code> and <code>44</code>, have single integer paths. <code>22</code> and <code>33</code> have paths composed of two integers. The first integer is the index to the two-element child vector, while the second integer is the index to their respective position in that child vector. Paths to elements nested in maps work similarly.</p><pre><code>(all-paths {:a [11 22], :b [33 [44] {:c 55}]})
;; => [{:path [], :value {:a [11 22], :b [33 [44] {:c 55}]}}
;;     {:path [:a], :value [11 22]} {:path [:a 0], :value 11}
;;     {:path [:a 1], :value 22}
;;     {:path [:b], :value [33 [44] {:c 55}]}
;;     {:path [:b 0], :value 33} {:path [:b 1], :value [44]}
;;     {:path [:b 1 0], :value 44} {:path [:b 2], :value {:c 55}}
;;     {:path [:b 2 :c], :value 55}]</code></pre><p>Each element, scalar or collection, has a unique, unambiguous path that tells you how to navigate to that value. For this heterogeneous, nested structure, some element's paths contain both integer indexes and keyword keys. Value <code>55</code> in this example is addressed by key <code>:b</code> at the top-level map, index <code>2</code> within that vector, and finally at key <code>:c</code> within that nested map.</p><p>There is no conceptual limit on how deep the nesting, nor on type of the containers. Speculoos handles each of Clojure's data collections: vectors, maps, lists, and sets.</p><pre><code>(all-paths [11 {:a 22, :b (list 33 #{44})}])
;; => [{:path [], :value [11 {:a 22, :b (33 #{44})}]}
;;     {:path [0], :value 11}
;;     {:path [1], :value {:a 22, :b (33 #{44})}}
;;     {:path [1 :a], :value 22} {:path [1 :b], :value (33 #{44})}
;;     {:path [1 :b 0], :value 33} {:path [1 :b 1], :value #{44}}
;;     {:path [1 :b 1 44], :value 44}]</code></pre><p>Notice how there is a single, unified path to every element, regardless of its container type. Vectors and list elements are addressed by an integer index. Map elements are addressed by their key. Map keys are often Clojure keywords, but we've seen that they can be any value such as integers, or even composite data types. Sets elements are addressed by their identities. So in the example above, <code>44</code> is located at path <code>[1 :b 1 44]</code>, where the tail value is not an index nor a key, but the element's identity. Admittedly, addressing elements in a set can be a little like herding cats, but it's still useful to have the capability. Wrangling sets merits its own <a href="#sets">dedicated section</a>.</p><p>With an element's path, you can precisely inspect its value. Let's pull out that <code>99</code>.</p><pre><code>(get-in* {:x 11,
          :y (list 22 33 [44 {:z [55 66 77 [88 {:q 99}]]}])}
         [:y 2 1 :z 3 1 :q])
;; => 99</code></pre><p>Speculoos' family of starred functions can get, associate, update, and dissociate any value of any type in a heterogeneous, arbitrarily-nested data structure. Later, we'll <a href="#fn-in*">discuss a little more</a> about the starred functions. For now, know that much of Speculoos' functionality relies on navigating paths. Once we've gathered the paths for a piece of data, and a specification composed of predicates arranged in a data structure that mimics that data, validation is merely a process of systematically applying each predicate to its respective scalar value in the data.</p><pre><code>(def data {:a 11, :b [22/7]})</code><br><code>(def scalar-spec {:a int?, :b [decimal?]})</code><br><code>;; equivalent to (int? 11)</code><code>((get-in* scalar-spec [:a]) (get-in* data [:a])) ;; => true</code><br><code>;; equivalent to (decimal? 22/7)</code><code>((get-in* scalar-spec [:b 0]) (get-in* data [:b 0]))
;; => false</code></pre><p>Speculoos' <code>validate-scalars</code> does exactly that.</p><pre><code>(validate-scalars data scalar-spec)
;; => [{:datum 11,
;;      :path [:a],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:datum 22/7,
;;      :path [:b 0],
;;      :predicate #function [clojure.core/decimal?],
;;      :valid? false}]</code></pre><p>Speculoos' <code>valid…?</code> series of functions are simply conveniences that return <code>true</code> if all the supplied predicates are satisfied.</p><p>Distinct from validating scalars, Speculoos can validate properties of a collection while ignoring the elements it contains. Recall that every element of a heterogeneous, arbitrarily-nested collection owns a path, not only the scalar elements. Consider this vector containing an empty map at index <code>0</code> and an empty list at index <code>1</code>, a total of three elements, each with its own path.</p><pre><code>(all-paths [{} '()])
;; => [{:path [], :value [{} ()]} {:path [0], :value {}}
;;     {:path [1], :value ()}]</code></pre><p>Validating a collection specification is analogous to validating a scalar specification, with the difference being that the predicate is applied to the parent container. Mechanistically, the collection predicate is applied to the entity located at <code>(drop-last predicate-path)</code>.<label class="margin-toggle sidenote-number" for="more-details"></label><input class="margin-toggle" id="more-details" type="checkbox"><span class="sidenote">Speculoos collection validation has a few more additional rules to make it completely generic, but let&apos;s consider only this bit for now.</span></input></p><pre><code>(def data [{:foo "bar"} '(:baz)])</code><code>(def count-is-two? #(= 2 (count %)))</code><code>(def collection-spec [count-is-two?])</code><br><code>(def path-to-element [])</code><code>(def path-to-predicate [0])</code><br><code>(get-in* data path-to-element) ;; => [{:foo "bar"} (:baz)]</code><br><code>(get-in* collection-spec path-to-predicate)
;; => #function [documentation/count-is-two?]</code></pre><p>The sole collection predicate in the specification <code>count-is-two?</code> is located at path <code>[0]</code> whereas the collection to be tested is located at <code>(drop-last [0])</code>.</p><pre><code>;; equivalent to (count-is-two? [{:foo "bar"} '(:baz)])</code><code>((get-in* collection-spec path-to-predicate)
  (get-in* data path-to-element))
;; => true</code></pre><p>We invoke the predicate, which tests if a collection has a count equal to two, with the vector as the sole argument. In this case, the predicate returns <code>true</code>. I've hand-waived too much, so now would be a good time be more explicit.</p></section><section id="principles"><h2>Speculoos working principles</h2><p>So you've got some Clojure data, and you want to check it somehow. Awesome. Maybe Speculoos can help. Let's understand Speculoos' principles.</p><p>Speculoos explicitly distinguishes specifying and validating scalars (numbers, strings, booleans, etc.) and collections (vectors, maps, lists, sequences, and sets). I visualize the distinction like this.<label class="margin-toggle sidenote-number" for="color-blind"></label><input class="margin-toggle" id="color-blind" type="checkbox"><span class="sidenote">Advance apologies to color-blind readers. (I myself am partially colorblind.) But text has only limited information channels after the character forms and color.</span></input></p><div class="side-by-side-container"><div class="side-by-side tight"><p>Scalar specification <span class="highlight">targets</span></p><code><span class="de-highlight">[</span><span class="highlight">42 \z </span><span class="de-highlight">{</span><span class="highlight">:a true :b 22/7</span><span class="de-highlight">}]</span></code></div><div class="side-by-side tight"><p>Collection specification <span class="highlight">targets</span></p><code><span class="highlight">[</span><span class="de-highlight">42 \z </span><span class="highlight">{</span><span class="de-highlight">:a true :b 22/7</span><span class="highlight">}]</span></code></div></div><p>They compliment each other. Scalar specifications target the data elements. Collection specifications target the organizers. Employing both, you can specify nearly any aspect of your heterogeneous, arbitrarily-nested data structure.</p><p>Speculoos specifications mimic the shape of your data. Validating a vector's scalars involves a specification that's also a vector.</p><pre><code>(def vector-data ["water" "coffee" "tea"])</code><br><code>(def vector-scalar-spec
  [predicate-1 predicate-2 predicate-3])</code></pre><p>A collection specification for a validating a map is itself a map.</p><pre><code>(def map-data {:a "water", :b ["coffee" "tea"]})</code><br><code>(def map-collection-spec
  {:check-keys predicate-4, :b [predicate-1 predicate-2]})</code></pre><p>You could even copy-paste your data and instruct your text editor to replace the datums with your choice of predicates. But don't let me box you in. Speculoos specifications are merely Clojure data structures and Speculoos predicates are merely Clojure functions. Compose your Speculoos specification with whatever tools you know and love. To augment <code>clojure.core</code>'s offerings, I wrote a <a href="#fn-in*">handful</a> of my own that appeal to the particular way my brain works.</p><p>Speculoos only validates where and when you want. Speculoos will simply ignore any datum that does not have a corresponding predicate.</p><pre><code>(valid-scalars? {:a 42, :not-checked "blah blah"} {:a int?})
;; => true</code><br><code>(def all-ints? #(every? int? %))</code><br><code>(valid-collections? [[1 2] [:not-checked] [3 4]]
                    [[all-ints?] [] [all-ints?]])
;; => true</code></pre><p>If you want to temporarily relax a specification, merely <code>assoc</code> or <code>dissoc</code> that particular predicate.</p><pre><code>(def list-data (list 'sym 22/7 true))</code><code>(def list-scalar-spec (list symbol? ratio? string?))</code><br><code>(valid-scalars? list-data list-scalar-spec) ;; => false</code><br><code>(def relaxed-spec (dissoc-in* list-scalar-spec [2]))</code><code>(valid-scalars? list-data relaxed-spec) ;; => true</code></pre><p>Your data and specification haven't changed and you can get on with your day.</p></section><section id="core"><h2>Core functions</h2><p>The <code>speculoos.core</code> namespace provides functions for validating Clojure data structures. Functions whose names start <em>validate-…</em> systematically apply predicates, arranged in a prescribed pattern in your specification, to elements of your data. The return value is an exhaustive sequence of all datum/predicate pairs, their path, and whether the predicate was satisfied. For example, we can validate with a scalar specification</p><pre><code>(validate-scalars ["mountains" :beach 'forest]
                  [string? keyword? string?])
;; => [{:datum "mountains",
;;      :path [0],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? true}
;;     {:datum :beach,
;;      :path [1],
;;      :predicate #function [clojure.core/keyword?],
;;      :valid? true}
;;     {:datum forest,
;;      :path [2],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? false}]</code></pre><p>and obtain three evaluations of the three datums. We see that the first two datums are valid, while the third datum is invalid (i.e. <code>:valid?</code> is <code>false</code>) because <code>'forest</code> at path <code>[2]</code> is not a string. This namespace contains <code>validate-…</code> variants for scalars and collections.</p><p>Sometimes, we just want a high-level <em>yes/no</em> whether the data structure as a whole satisfies all of the predicates. Functions named with the pattern <em>valid-…?</em> return a simple boolean, dependent on whether all the specified predicates are satisfied.</p><pre><code>(valid-scalars? ["mountains" :beach 'forest]
                [string? keyword? string?])
;; => false</code></pre><p>With the same data and specification as before, we see that our data is indeed invalid. Similarly to the validation functions, the namespace provides functions named with variations of <code>valid…?</code> for both scalars and collections.</p><p>Speculoos validates scalars contained in maps with the same principle: make your specification look like the data, replacing the scalar with your choice of predicate. Here's a quick example.</p><pre><code>(valid-scalars? {:first-name "Sherlock",
                 :last-name "Holmes",
                 :address {:street-name "Baker Street",
                           :street-number [221 'B]}}
                {:first-name string?,
                 :last-name string?,
                 :address {:street-name string?,
                           :street-number [int?],
                           :city string?}})
;; => true</code></pre><p>Predicates in the specification share the same keyword as the value they validate. Any datum that does not have a corresponding key-value pair in the predicate will not be validated. In this example, the second element of the <code>:street-number</code> is not validated because <code>'B</code> does not have a corresponding predicate in that path of the specification. Likewise, any predicate that does not have a corresponding datum will also not be validated. In this example, <code>:city</code>'s predicate, <code>string?</code>, will not be used for validation because the data does not have a corresponding datum at that path.</p><p>You're probably tired of me mentioning how Speculoos splits up scalar and collection validation, but here's a twist: Speculoos has a pair of convenience functions that do both. As a consolation to having to write two specifications for each data structure, I reserved the best, i.e., shortest, function names for doing both with one invocation. Supply data, then a scalar specification, followed by a collection specification.</p><pre><code>(defn length-four? [v] (= 4 (count v)))</code><br><code>(valid? [42 :foo \z] [int? keyword? char?] [length-four?])
;; => false</code></pre><p>Plain <code>valid?</code> performs a scalar validation immediately followed by a collection validation and returns a single boolean. In this example, three scalars were validated followed by one collection validation. For a little more detail,</p><pre><code>(validate [42 :foo \z] [int? keyword? char?] [length-four?])
;; => ({:datum 42,
;;      :path [0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:datum :foo,
;;      :path [1],
;;      :predicate #function [clojure.core/keyword?],
;;      :valid? true}
;;     {:datum \z,
;;      :path [2],
;;      :predicate #function [clojure.core/char?--5473],
;;      :valid? true}
;;     {:datum [42 :foo \z],
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? false,
;;      :value #function [documentation/length-four?]})</code></pre><p><code>validate</code> generates an itemized sequence of all the scalar validations followed by all the collection validations. We can see that our data did not satisfy the collection specification: Our specification required the vector contain exactly four elements, whereas our vector contains only three.</p><p>It's tedious to manually scan validation results, so Speculoos supplies a helper function to show only the invalid results.</p><pre><code>(only-invalid (validate [42 :foo \z]
                        [int? keyword? char?]
                        [length-four?]))
;; => ({:datum [42 :foo \z],
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? false,
;;      :value #function [documentation/length-four?]})</code></pre><p>Speculoos validates sequences by applying predicates, located in the specification, to the sequence located at the path of its parent. In this instance, the predicate <code>length-four?</code> is found at path <code>[0]</code> in the specification, but is applied to the path <code>[]</code> of the data, which is the three-element vector.<label class="margin-toggle sidenote-number" for="examine-soon"></label><input class="margin-toggle" id="examine-soon" type="checkbox"><span class="sidenote">This invalid result contains a key-value pair that we haven&apos;t yet discussed, an <em>ordinal parent path</em>, which we will examine more closely in a few paragraphs.</span></input></p><p>Let's try another tinted text illustration to compare the differences between applying predicates to scalars and collections.<div class="side-by-side-container"><div class="side-by-side tight"><p>Data scalars, top</p><pre><code><span class="de-highlight">[</span><span class="highlight c1">42   </span><span class="highlight c2">:foo    </span><span class="highlight c3">\z    </span><span class="de-highlight">]</span></code><br><code><span class="de-highlight">[</span><span class="highlight c1">int? </span><span class="highlight c2">keyword? </span><span class="highlight c3">char?</span><span class="de-highlight">]</span></code></pre><p>Scalar specification predicates, bottom</p></div><div class="side-by-side tight"><p>Data sequence, top</p><pre><code><span class="highlight c4">[</span><span class="de-highlight">42 </span><span class="de-highlight">:foo </span><span class="de-highlight">\z    </span><span class="highlight c4">]</span></code><br><code><span class="de-highlight">[</span><span class="highlight c4">length-four?</span><span class="de-highlight">]</span></code></pre><p>Collection specification predicate, bottom</p></div></div></p><p>The difference is subtle. The scalar predicates (<span class="c1">red</span>, <span class="c2">blue</span>, and <span class="c3">green</span> in the left column) share the exact same paths as their target elements in the data vector. <code class="c1">int?</code> applies to <code class="c1">42</code>, <code class="c2">keyword?</code> applies to <code class="c2">:foo</code>, and <code class="c3">char?</code> applies to <code class="c3">\z</code>. The predicate for the collection validation, <span class="c4">length-four?</span>, contained <em>within</em> the specification vector, applies the data vector itself. That's an ongoing theme: scalar predicates apply to the datum that shares their path, collection predicates apply to the parent container.</p><p>Let's set aside the scalar validation component and focus on collection validation. Pretend we want to validate a four-element vector with at least one element being a keyword.<label class="margin-toggle sidenote-number" for="reasonable"></label><input class="margin-toggle" id="reasonable" type="checkbox"><span class="sidenote">You might reasonably think that testing for a keyword is a scalar specification. But in this case, we are testing for a <em>keyword somewhere in a collection</em>, so it&apos;s a property of the collection.</span></input> First, we'll set up our predicates.</p><pre><code>(defn length-four? [v] (= 4 (count v)))</code><code>(defn at-least-one-kw? [v] (some keyword? v))</code><code>(defn first-greater-than-fourth? [v] (> (v 0) (v 3)))</code></pre><p>The first wrinkle is that <em>every</em> predicate in a collection specification, no matter how few nor how many, applies to the immediate parent container in the data. Let's validate the collection against three predicates using Speculoos' dedicated collection validation function.</p><pre><code>(validate-collections [42 "abc" 'foo 22/7]
                      [length-four? at-least-one-kw?
                       first-greater-than-fourth?])
;; => ({:datum [42 "abc" foo 22/7],
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? true,
;;      :value #function [documentation/length-four?]}
;;     {:datum [42 "abc" foo 22/7],
;;      :ordinal-parent-path [],
;;      :path [1],
;;      :valid? nil,
;;      :value #function [documentation/at-least-one-kw?]}
;;     {:datum [42 "abc" foo 22/7],
;;      :ordinal-parent-path [],
;;      :path [2],
;;      :valid? true,
;;      :value #function
;;              [documentation/first-greater-than-fourth?]})</code></pre><p>Speculoos validates all three predicates against the collection. <code>length-four?</code> is satisfied because <code>[42 "abc" 'foo 227]</code> contains four elements, <code>first-greater-than-fourth?</code> is satisfied because the first element is greater than the fourth element, but <code>at-least-one-kw?</code> is not satisfied because there is not at least one keyword. All three collection collection predicates were applied to the single container vector.</p><p>The second wrinkle involves how Speculoos determines where to find the parent collection sequence. Every predicate in the collection specification applies to its parent, <em>regardless of the presence of any intervening predicates or collections at that level of nesting</em>. So that the next example highlights what we're interested in, we'll bury our vector of interest into some nested mess where we don't care about any of the other stuff.</p><pre><code>(def data-1 [[] [] [] [[] [] [42 "abc" 'foo 22/7]]])</code></pre><p>Okay, we've got to look sharp; our target vector is two levels deep. Speculoos' mantra is <em>Make your specification shaped like your data</em>. For a Speculoos scalar specification there is a nearly one-to-one correspondence between the data and the specification. The mantra holds for Speculoos collection specification, but the correspondence is slightly weaker. We want our collection specification to mimic the data's square brackets, preserving the order, but Speculoos collection validation does not require a predicate for every collection. In this example, we don't care about all those empty vectors, so we simply leave their counterparts in the specification empty. We only put predicates into the collection that we want to validate.</p><pre><code>(def spec-1
  [[] [] []
   [[] []
    [length-four? at-least-one-kw?
     first-greater-than-fourth?]]])</code><br><code>(validate-collections data-1 spec-1)
;; => ({:datum [42 "abc" foo 22/7],
;;      :ordinal-parent-path [3 2],
;;      :path [3 2 0],
;;      :valid? true,
;;      :value #function [documentation/length-four?]}
;;     {:datum [42 "abc" foo 22/7],
;;      :ordinal-parent-path [3 2],
;;      :path [3 2 1],
;;      :valid? nil,
;;      :value #function [documentation/at-least-one-kw?]}
;;     {:datum [42 "abc" foo 22/7],
;;      :ordinal-parent-path [3 2],
;;      :path [3 2 2],
;;      :valid? true,
;;      :value #function
;;              [documentation/first-greater-than-fourth?]})</code></pre><p>As before, our vector satisfies our first and third collection predicates, while the second predicate is not satisfied because it does not contain at least one keyword. And now we can see a better demonstration of the ordinal parent paths. The predicates live at paths <code>[3 2 0]</code>, <code>[3 2 1]</code>, and <code>[3 2 2]</code>, respectively, in <code>spec-1</code>. But they all apply to the <em>vector</em> that lives at <code>[3 2]</code> in <code>data-1</code>.</p><p>Let's combine two principles: Speculoos applies collection predicates to the parent container, and all collection predicates apply to the parent. Speculoos does not require the predicates to live at any particular index within the specification. Validating sequences can therefore involve an interrupted stream of collection predicates.</p><pre><code>(def data-2 [42 "abc" 'foo 22/7])</code><br><code>(def spec-2
  [length-four? [] at-least-one-kw? []
   first-greater-than-fourth?])</code><br><code>(validate-collections data-2 spec-2)
;; => ({:datum [42 "abc" foo 22/7],
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? true,
;;      :value #function [documentation/length-four?]}
;;     {:datum [42 "abc" foo 22/7],
;;      :ordinal-parent-path [],
;;      :path [2],
;;      :valid? nil,
;;      :value #function [documentation/at-least-one-kw?]}
;;     {:datum [42 "abc" foo 22/7],
;;      :ordinal-parent-path [],
;;      :path [4],
;;      :valid? true,
;;      :value #function
;;              [documentation/first-greater-than-fourth?]})</code></pre><p>Validation skips over the empty vectors in the specification, in this example for two reasons. First, those nested vectors in the specification do not contain a predicate. But, second,  even if <code>spec-2</code> did contain those predicates, <code>data-2</code> possesses no corresponding nested vectors to test. Speculoos offers this flexibility in arranging your collection predicates so that the system is completely general. In practice, I recommend that you place all your collection predicates as close as possible to the head of the sequence where they apply. That convention will help future human readers of your specification.</p><p>So far, we've seen the <em>parent</em> and <em>path</em> parts, but let's do a final example that demonstrates <em>ordinal</em>. First, let's create some length-checking predicates with nice names that are easy to track.</p><pre><code>(defn cero? [v] (= 0 (count v)))</code><code>(defn uno? [v] (= 1 (count v)))</code><code>(defn dos? [v] (= 2 (count v)))</code><code>(defn tres? [v] (= 3 (count v)))</code></pre><p>Our example data will be a series of different sized vectors nested within a parent vector.</p><pre><code>(def data-3
  ['_ [] '_ [:one] '_ [:two :three] '_ [:four :five :six]])</code></pre><p>Our collection specification will test for the lengths of the nested vectors, interleaved with predicates that will test properties of the parent vector itself. Our length-checking predicates are contained within the collection to which they apply, in the order in which they appear in data.</p><pre><code>(def spec-3
  [[cero?] vector? [uno?] empty? [dos?] list? [tres?] map?])</code></pre><p>Speculoos validates collection predicates against their parent, so <code>vector?</code>, <code>empty?</code>, <code>list?</code>, and <code>map?</code> apply to the parent vector. The length-checking predicates apply to their nested child vectors, in order.</p><pre><code>(validate-collections data-3 spec-3)
;; => ({:datum [],
;;      :ordinal-parent-path [0],
;;      :path [0 0],
;;      :valid? true,
;;      :value #function [documentation/cero?]}
;;     {:datum [_ [] _ [:one] _ [:two :three] _
;;              [:four :five :six]],
;;      :ordinal-parent-path [],
;;      :path [1],
;;      :valid? true,
;;      :value #function [clojure.core/vector?--5479]}
;;     {:datum [:one],
;;      :ordinal-parent-path [1],
;;      :path [2 0],
;;      :valid? true,
;;      :value #function [documentation/uno?]}
;;     {:datum [_ [] _ [:one] _ [:two :three] _
;;              [:four :five :six]],
;;      :ordinal-parent-path [],
;;      :path [3],
;;      :valid? false,
;;      :value #function [clojure.core/empty?]}
;;     {:datum [:two :three],
;;      :ordinal-parent-path [2],
;;      :path [4 0],
;;      :valid? true,
;;      :value #function [documentation/dos?]}
;;     {:datum [_ [] _ [:one] _ [:two :three] _
;;              [:four :five :six]],
;;      :ordinal-parent-path [],
;;      :path [5],
;;      :valid? false,
;;      :value #function [clojure.core/list?]}
;;     {:datum [:four :five :six],
;;      :ordinal-parent-path [3],
;;      :path [6 0],
;;      :valid? true,
;;      :value #function [documentation/tres?]}
;;     {:datum [_ [] _ [:one] _ [:two :three] _
;;              [:four :five :six]],
;;      :ordinal-parent-path [],
;;      :path [7],
;;      :valid? false,
;;      :value #function [clojure.core/map?--5477]})</code></pre><p>Okay. Eight predicates in the collection specification, eight validation results. That's a good sanity check. Let's inspect only the valid results.</p><pre><code>(require '[speculoos.core :refer [only-valid]])</code><br><code>(only-valid (validate-collections data-3 spec-3))
;; => ({:datum [],
;;      :ordinal-parent-path [0],
;;      :path [0 0],
;;      :valid? true,
;;      :value #function [documentation/cero?]}
;;     {:datum [_ [] _ [:one] _ [:two :three] _
;;              [:four :five :six]],
;;      :ordinal-parent-path [],
;;      :path [1],
;;      :valid? true,
;;      :value #function [clojure.core/vector?--5479]}
;;     {:datum [:one],
;;      :ordinal-parent-path [1],
;;      :path [2 0],
;;      :valid? true,
;;      :value #function [documentation/uno?]}
;;     {:datum [:two :three],
;;      :ordinal-parent-path [2],
;;      :path [4 0],
;;      :valid? true,
;;      :value #function [documentation/dos?]}
;;     {:datum [:four :five :six],
;;      :ordinal-parent-path [3],
;;      :path [6 0],
;;      :valid? true,
;;      :value #function [documentation/tres?]})</code></pre><p>We see each of the nested children vectors satisfy their predicates, but more interestingly, we see how Speculoos located them. The <code>:path</code> key-value pair reports where Speculoos found the collection predicate within <code>spec-3</code>. The <code>ordinal-parent-path</code> indicates where Speculoos applied the predicates within <code>data-3</code>. For example, <code>[:four :five :six]</code> resides at ordinal parent path <code>[3]</code>, which means that it is the fourth nested collection<label class="margin-toggle sidenote-number" for="zero-indexed"></label><input class="margin-toggle" id="zero-indexed" type="checkbox"><span class="sidenote">Zero-indexed.</span></input> within the parent sequence. Predicate <code>dos?</code> located at path <code>[4 0]</code> in <code>spec-3</code> was applied to <code>[:two :three]</code> the third collection of the parent sequence, and therefore an ordinal parent path <code>[2]</code>.</p><p>And now, let's inspect the invalid results.</p><pre><code>(only-invalid (validate-collections data-3 spec-3))
;; => ({:datum [_ [] _ [:one] _ [:two :three] _
;;              [:four :five :six]],
;;      :ordinal-parent-path [],
;;      :path [3],
;;      :valid? false,
;;      :value #function [clojure.core/empty?]}
;;     {:datum [_ [] _ [:one] _ [:two :three] _
;;              [:four :five :six]],
;;      :ordinal-parent-path [],
;;      :path [5],
;;      :valid? false,
;;      :value #function [clojure.core/list?]}
;;     {:datum [_ [] _ [:one] _ [:two :three] _
;;              [:four :five :six]],
;;      :ordinal-parent-path [],
;;      :path [7],
;;      :valid? false,
;;      :value #function [clojure.core/map?--5477]})</code></pre><p>These three predicates were interleaved among the nested child vectors, so they all apply to the parent sequence. Their ordinal parent paths are all identical, <code>[]</code> which points to <code>data-3</code>'s root. This illustrates how Speculoos applies all collection predicates to their parent collection, regardless of their position within the sequence.</p><p>You may find a situation where it's most natural to specify a sequence with regular expression style, so Speculoos provides a toy utility for that job. <code>seq-regex</code> accepts a sequence followed by pairs of predicates and regex-like operators.</p><pre><code>(require '[speculoos.utility :refer [seq-regex]])</code><br><code>(seq-regex [42 :foo :bar :baz 22/7]
           int?
           :.
           keyword?
           [1 3]
           ratio?
           :?)
;; => true</code></pre><p>In this example, we asked if our sequence contains exactly one integer, followed by one to three keywords, followed by zero or one ratios. In a collection validation, <code>seq-regex</code> could be useful like this.</p><pre><code>(valid-collections?
  {:a [1 2 3 'foo 'bar \c \l \o \j \u \r \e]}
  {:a [#(seq-regex % int? [0 3] symbol? :+ char? :*)]})
;; => true</code></pre><p>The nested vector does indeed hold zero to three integers, followed by one or more symbols, followed by any number of characters.</p><p>Hey, maps are collections, too. Speculoos has got a story for maps that rhymes. A map specification looks mostly like the data, predicates apply to the containing parent, and any number of predicates may be applied. Let's go.</p><pre><code>(def data-4 {:the-clown "Bozo"})</code><br><code>(def red-nose? #(contains? % :the-clown))</code><code>(def spec-4 {:foo red-nose?, :whoa-nellie not-empty})</code><br><code>(validate-collections data-4 spec-4)
;; => ({:datum {:the-clown "Bozo"},
;;      :ordinal-parent-path [],
;;      :path [:foo],
;;      :valid? true,
;;      :value #function [documentation/red-nose?]}
;;     {:datum {:the-clown "Bozo"},
;;      :ordinal-parent-path [],
;;      :path [:whoa-nellie],
;;      :valid? {:the-clown "Bozo"},
;;      :value #function [clojure.core/not-empty]})</code></pre><p>After bashing out scalar specifications for sequences, creating a map's collection specification takes a little getting used to. During map validation, Speculoos searches for predicate located at keys <em>not</em> found in the data. Those predicates validate the containing map. If a map specification's key <em>is</em> in the data set, then Speculoos dives into the value associated with that key. In the context of a collection specification, that value must be a collection.</p><pre><code>(def stop-and-smell
  {:yellow ["dandelion" "buttercup" "marigold"],
   :red ["poppy" "rose"],
   :blue ["bluebell"],
   :purple ["violet"]})</code><br><code>(def valid-color?
  #(every? #{:red :orange :yellow :green :blue :purple}
           (keys %)))</code><br><code>(def flowers-spec
  {:yellow [#(every? string? %)],
   :red [#(= 2 (count %))],
   :colors-spec valid-color?})</code><br><code>(validate-collections stop-and-smell flowers-spec)
;; => ({:datum ["dandelion" "buttercup" "marigold"],
;;      :ordinal-parent-path [:yellow],
;;      :path [:yellow 0],
;;      :valid? true,
;;      :value #function [documentation/fn--138374]}
;;     {:datum ["poppy" "rose"],
;;      :ordinal-parent-path [:red],
;;      :path [:red 0],
;;      :valid? true,
;;      :value #function [documentation/fn--138376]}
;;     {:datum {:blue ["bluebell"],
;;              :purple ["violet"],
;;              :red ["poppy" "rose"],
;;              :yellow ["dandelion" "buttercup" "marigold"]},
;;      :ordinal-parent-path [],
;;      :path [:colors-spec],
;;      :valid? true,
;;      :value #function [documentation/valid-color?]})</code></pre><p><code>flowers-spec</code> contains a total of three predicates. The <code>valid-color?</code> predicate applies to the root <code>stop-and-smell</code> map because the key <code>:colors-spec</code> does not appear in the data. Two predicates, located at shared keys <code>:yellow</code> and <code>red</code> apply to nested vectors because those keys are shared between the data and specification, and the values at those keys are themselves collections.</p><p>Don't create a <code>does-not-contain-kw</code> or some other predicate that would restrict the keys a map <em>might</em> contain. It's better to gracefully ignore map elements that don't concern you. Speculoos does not have any code branches that would prohibit you from doing this, so be a good neighbor.</p><p>Now that we've seen an example of validating vectors nested in a map, let's compare that to validating maps nested in a vector. Let's test whether a nested map contains <code>:first-name</code> and <code>:last-name</code> keys.</p><pre><code>(def person-spec
  #(and (contains? % :first-name) (contains? % :last-name)))</code></pre><p>Our data is a sequence containing two maps that describe two missions of the US Apollo moon project. We'll target the <code>:commander</code> entries of the two mission maps. Each valid commander map should contain both a <code>:first-name</code> and a <code>:last-name</code> entry, though as Speculoos will report in a moment, one of the entries does not.</p><pre><code>(def apollo
  [{:mission 12,
    :CM "Yankee Clipper",
    :LM "Intrepid",
    :commander {:first-name "Charles",
                :last-name "Conrad",
                :nick-name "Pete"},
    :cm-pilot {:first-name "Richard",
               :last-name "Gordon",
               :nick-name "Dick"},
    :lm-pilot {:first-name "Alan",
               :last-name "Bean",
               :nick-name "Al"}}
   {:mission 17,
    :CM "America",
    :LM "Challenger",
    :commander {:nick-name "Gene", :last-name "Cernan"},
    :cm-pilot {:nick-name "Ron", :last-name "Evans"},
    :lm-pilot {:nick-name "Harry", :last-name "Schmitt"}}])</code></pre><p>The collection specification for validating the nested map entries is deceptively simple: Make the specification look <em>mostly</em> like the data, inserting predicates at points we want to test. So we start with a root vector to hold everything. Then, we insert two maps to correspond to the two mission maps.</p><pre><code>(def apollo-spec
  [{:commander {:check-person person-spec}}
   {:commander {:check-person person-spec}}])</code></pre><p>We've got to get the keywords correct so that we validate what we want. First, within the mission map, we use the identical key<code>:commander</code> because Speculoos requires that to navigate to the correct commander map. Once Speculoos has got hold of the commander map to validate, we use a key that doesn't appear in the data, <code>:check-person</code> is an informative label, to indicate to Speculoos that we've arrived at the map we want to validate. In this example, <code>person-spec</code> will be applied to </p><pre><code>{:first-name "Charles" :last-name "Conrad" :nick-name "Pete"}</code></pre><p> and </p><pre><code>{:nick-name "Gene" :last-name "Cernan"}.</code></pre><p>And indeed, validating the data against our specification shows us that our second commander map is invalid.</p><pre><code>(validate-collections apollo apollo-spec)
;; => ({:datum {:first-name "Charles",
;;              :last-name "Conrad",
;;              :nick-name "Pete"},
;;      :ordinal-parent-path [0 :commander],
;;      :path [0 :commander :check-person],
;;      :valid? true,
;;      :value #function [documentation/person-spec]}
;;     {:datum {:last-name "Cernan", :nick-name "Gene"},
;;      :ordinal-parent-path [1 :commander],
;;      :path [1 :commander :check-person],
;;      :valid? false,
;;      :value #function [documentation/person-spec]})</code></pre><p>The second commander map does not contain a <code>:first-name</code> key. Note this validation would be impossible with a scalar specification because Speculoos considers presence or absence of a map's key to be a property of the collection, not of the scalar itself.</p><p>Let's wrap up this section on collection specification by making some cheat sheets. The guidelines for scalar specification fit on the back of a business card.</p><div class="business-card-container"><div class="business-card"><p class="centered"><em>Speculoos scalar specifications</em><ol><li>Make the specification mimic the data's shape.</li><li>Put predicates in the corresponding datum spots to validate.</li></ol></p></div></div><p>The guidelines for collection specification require a business card plus a sticky note.</p><div class="business-card-container"><div class="business-card"><p class="centered"><em>Speculoos collection specifications</em></p><ol><li>Make the specification mimic the data's shape.</li><li>Put predicates <em>inside</em> the collection to be validated.</li></ol></div></div><br><div class="business-card-container"><div class="business-card sticky-note"><p class="centered"><em>Speculoos collection<br>specifications, cont.</em></p><ol><li><em>Sequences:</em> Predicates in nested collections are applied in order of their parent collection, regardless of any intervening entities.</li><li><em>Maps:</em> Predicates at keys that do not appear in the data are applied to the containing map.</li></ol></div></div></section><section id="fn-in*"><h2><code>fn-in*</code></h2><p>Speculoos' implementation and developer interface lean heavily on the concept of a <a href="#path">path</a>. Paths unambiguously point to datums and predicates, and thereby enable applying a predicate to that datum. Also, a path is an important component of the validation reports, giving the precise location of datums, and, when they differ in a collection specification, the predicate. Given the importance of this path concept, Speculoos provides an auxiliary set of tools to inspect, change, and remove elements in a heterogeneous, arbitrarily-nested data structure. These functions are modeled after <code>clojore.core/get-in</code>, <code>assoc-in</code>, <code>update-in</code>, and <code>dissoc</code>.<label class="margin-toggle sidenote-number" for="dissoc-in"></label><input class="margin-toggle" id="dissoc-in" type="checkbox"><span class="sidenote"><code>clojure.core</code> does <a href="https://ask.clojure.org/index.php/730/missing-dissoc-in">not provide</a> an equivalent <code>dissoc-in</code>.</span></input> The starred versions of their <code>clojure.core</code> namesakes consume identical argument lists, but have the ability to seamlessly navigate any of the Clojure data structures (vectors, lists, maps, and sets) in any nesting pattern.</p><p>Here's how paths work. Vectors are indexed by zero-based integers.</p><pre><code>           [100 101 102 103]</code><code>indexes --> 0   1   2   3</code></pre><p>Same for lists.</p><pre><code>          '(97 98 99 100)</code><code>indexes --> 0  1  2  3</code></pre><p>Maps are addressed by their keys, which are often keywords, like this</p><pre><code>        {:a 1 :foo "bar" :hello 'world}</code><code>keys --> :a   :foo       :hello</code></pre><p>but maps may be keyed by any value, including integers</p><pre><code>        {0 "zero" 1 "one" 99 "ninety-nine"}</code><code>keys --> 0        1       99</code></pre><p>or any other value</p><pre><code>        {"a" :value-at-str-key-a 'b :value-at-sym-key-b \c :value-at-char-key-c}</code><code>keys --> "a"                     'b                     \c</code></pre><p>even composite values.</p><pre><code>        {[0] :val-at-vec-0 [1 2 3] :val-at-vec-1-2-3 {} :val-at-empty-map}</code><code>keys --> [0]               [1 2 3]                   {}</code></pre><p>Set elements are addressed by their identities, so they are located at themselves.</p><pre><code>             #{42 :foo true 22/7}</code><code>identities --> 42 :foo true 22/7</code></pre><p>A <em>path</em> is a vector of indexes, keys, or identities that allow the starred functions to dive into a nested data structure, one item per level of nesting.</p><pre><code>(require '[speculoos.fn-in :refer
           [get-in* assoc-in* update-in* dissoc-in*]])</code><br><code>(get-in* [1 2 3 [4 5 6 [7 8 9]]] [3 3 2]) ;; => 9</code></pre><p>The operation doesn't necessarily bottom out on a scalar value,</p><pre><code>(get-in* [1 2 3 [4 5 6 [7 8 9]]] [3 3]) ;; => [7 8 9]</code></pre><p>but could point to a nested collection.</p><p>Within a group of nested vectors, all components of the path are zero-based integers. Each integer serves as an index into a single depth.</p><p>Nested lists are similar.</p><pre><code>(get-in* '(1 2 (3 4 (5 6) 7) 8) [2 2 1]) ;; => 6</code></pre><p>Within a group of nested maps, all components of the path are keys. In this example, all the keys are keywords.</p><pre><code>(get-in* {:a 11, :b {:c 22, :d {:e 33, :f {:g 44}}}}
         [:b :d :f :g])
;; => 44</code></pre><p>Nested maps with integer keys.</p><pre><code>(get-in* {99 "ninety-nine",
          33 {44 "fourty-four", 111 {55 "fifty-five"}}}
         [33 111 55])
;; => "fifty-five"</code></pre><p>Nested map with a path composed of a composite key <code>[3]</code>, a symbol <code>'yipee</code> key, and a boolean <code>true</code> key, yielding a keyword.</p><pre><code>(get-in* {[3] {:a 44, 'yipee {true :ice-cream}}}
         [[3] 'yipee true])
;; => :ice-cream</code></pre><p>Set elements are addressed by their identities.</p><pre><code>(get-in* #{99 :foo \z} [:foo]) ;; => :foo</code></pre><p>The empty vector addresses the top-level root collection of any collection type.</p><pre><code>(get-in* [1 2 3] []) ;; => [1 2 3]</code><code>(get-in* '(:foo "bar" 42) []) ;; => (:foo "bar" 42)</code><code>(get-in* {:a 1, :b 2} []) ;; => {:a 1, :b 2}</code><code>(get-in* #{:foo 42 \z} []) ;; => #{42 :foo \z}</code></pre><p>We've just seen a truckload of examples of how we can get a value if we know its path. Also, with an element's path, we can associate a new value, swapping in an entirely new value.</p><pre><code>(assoc-in* [42 "abc" :foo] [2] \z) ;; => [42 "abc" \z]</code></pre><p>We can update the value, applying a function to the current value..</p><pre><code>(update-in* [42 "abc" :foo] [0] inc) ;; => [43 "abc" :foo]</code></pre><p>We can remove a value entirely.</p><pre><code>(dissoc-in* [42 "abc" :foo] [1]) ;; => [42 :foo]</code></pre><p>One of <code>spec.alpha</code>'s core features is returning <em>conformed</em> data, a version of the validated data that is annotated according to whichever optional predicate was satisfied. Speculoos does not attempt to replicate this feature. The <code>valid…?</code> function family returns simple <code>true/false</code> while the <code>validate…</code> function family returns reports which are strictly Clojure data.</p><pre><code>(validate [42 "abc"] [int? char?] [vector?])
;; => ({:datum 42,
;;      :path [0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:datum "abc",
;;      :path [1],
;;      :predicate #function [clojure.core/char?--5473],
;;      :valid? false}
;;     {:datum [42 "abc"],
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? true,
;;      :value #function [clojure.core/vector?--5479]})</code></pre><p>The validation report consists of a sequence of maps, one for each predicate/datum pair that holds the datum, the predicate, the path(s) where they are located within the data and specification, and the results of the evaluation. Most importantly, the validation report can be processed with any Clojure tools. Pull out paths using regular tools. For example, we can filter the previous result to see only unsatisfied predicates.<label class="margin-toggle sidenote-number" for="only-invalid"></label><input class="margin-toggle" id="only-invalid" type="checkbox"><span class="sidenote">In fact, this is substantially how <code>speculoos.core/only-invalid</code> works.</span></input></p><pre><code>(filter #(not (:valid? %))
  (validate [42 "abc"] [int? char?] [vector?]))
;; => ({:datum "abc",
;;      :path [1],
;;      :predicate #function [clojure.core/char?--5473],
;;      :valid? false})</code></pre><p>We can then use <code>get-in*</code> to get the path.</p><pre><code>(get-in* (filter #(not (:valid? %))
           (validate [42 "abc"] [int? char?] [vector?]))
         [0 :path])
;; => [1]</code></pre><p>With the path to the invalid datum and predicate, we could investigate the offending data, amend the data so that it validates, alter the specification so that the predicate is satisfied, or even remove the predicate so that it is not tested. The <a href="#utility"><code>speculoos.utility</code></a> namespace provides all those options.</p></section><section id="predicates"><h2>Predicates</h2><p>A predicate function returns a truthy or falsey value.</p><pre><code>(#(<= 5 %) 3) ;; => false</code><br><code>(#(= 3 (count %)) [1 2 3]) ;; => true</code></pre><p>Non-boolean returns work, too. For example, <a href="#sets">sets</a> make wonderful membership tests.</p><pre><code>;; truthy</code><code>(#{:blue :green :orange :purple :red :yellow} :green)
;; => :green</code><br><code>;; falsey</code><code>(#{:blue :green :orange :purple :red :yellow} :swim)
;; => nil</code></pre><p>Regular expressions come in handy for validating string contents.</p><pre><code>(re-find #"^Four" "Four score and seven years ago...")
;; => "Four"</code><br><code>(re-find #"^Four" "When in the course of human events...")
;; => nil</code></pre><p>Invoking a predicate when supplied with a datum — scalar or collection — is the core action of Speculoos' validation.</p><pre><code>(int? 42) ;; => true</code><br><code>(validate-scalars [42] [int?])
;; => [{:datum 42,
;;      :path [0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}]</code></pre><p>Speculoos is fairly ambivalent about the predicate return value. The <code>validate…</code> family of functions mindlessly churns through its sequence of predicate-datum pairs, evaluates them, and stuffs the results into <code>:valid?</code> keys. The <code>valid…?</code> family of functions rips through <em>that</em> sequence, and if none of the results are falsey, returns <code>true</code>, otherwise it returns <code>false</code>.</p><p>Often, we want to combine multiple predicates to make the validation more specific. We can certainly use <code>clojure.core/and</code></p><pre><code>#(and (int? %) (pos? %) (even? %))</code></pre><p> and <code>clojure.core/or</code></p><pre><code>#(or (string? %) (char? %))</code></pre><p>which have the benefit of being universally understood. But Clojure also provides a pair of nice functions that streamline the expression and convey your intention. <code>every-pred</code> composes an arbitrary number of predicates with <code>and</code> semantics.</p><pre><code>((every-pred number? pos? even?) 100) ;; => true</code></pre><p>Similarly, <code>some-fn</code> composes predicates with <code>or</code> semantics.</p><pre><code>((some-fn number? string? boolean?) \z) ;; => false</code></pre><p>When Speculoos validates the scalars of a sequence, it consumes each element in turn. If we care only about validating some of the elements, we must include placeholders in the specification to maintain the sequence of predicates. For example, suppose we only want to validate the third element of <code>[42 :foo \z]</code>; the first two elements are irrelevant to us. We have a few options. We could write our own little always-true predicate,<label class="margin-toggle sidenote-number" for="no-%"></label><input class="margin-toggle" id="no-%" type="checkbox"><span class="sidenote"><code>#(true)</code> won&apos;t work because <code>true</code> is not invocable. <code>#(identity true)</code> loses the conciseness.</span></input></p><pre><code>(fn [] true)</code></pre><p>but Clojure already has a couple of nice options.</p><pre><code>(valid-scalars? [42 :foo \z]
                [(constantly true) (constantly true) char?])
;; => true</code></pre><p><code>constantly</code> is nice because it accepts any number of args. But for my money, nothing tops <code>any?</code>.</p><pre><code>(valid-scalars? [42 :foo \z] [any? any? char?]) ;; => true</code></pre><p>It's four characters, doesn't require typing parentheses, and the everyday usage of <em>any</em> aligns well with its technical purpose.</p><p>A word of warning about <code>clojure.core/contains?</code>. It might seem natural to use <code>contains?</code> to check if a collection contains an item, but it doesn't do what its name suggests. Observe.</p><pre><code>(contains? [97 98 99] 1) ;; => true</code></pre><p><code>contains?</code> actually tells you whether a collection contains a key. For a vector, it tests for an index. If you'd like to check whether a value is contained in a collection, you can use this pattern.<label class="margin-toggle sidenote-number" for="in?"></label><input class="margin-toggle" id="in?" type="checkbox"><span class="sidenote">Check out <code>speculoos.utility/in?</code>.</span></input></p><pre><code>(defn in? [coll item] (some #(= item %) coll))</code><br><code>(in? [97 98 99] 98) ;; => true</code><br><code>(in? [97 98 99] 1) ;; => nil</code></pre><p>I've been using the <code>#(...)</code> form because it's compact, but it does have a drawback for use with Speculoos.</p><pre><code>[{:path [0], :datum 42, :predicate #function[documentation/eval94717/fn--94718], :valid? false}]</code></pre><p>The function rendering is not terribly informative when the validation displays the predicate. Same problem with <code>(fn [v] (...))</code>.</p><p>One solution to this issue is to define your predicates with an informative name.</p><pre><code>(def greater-than-50? #(< 50 %))</code><br><code>(validate-scalars [42] [greater-than-50?])
;; => [{:datum 42,
;;      :path [0],
;;      :predicate #function [documentation/greater-than-50?],
;;      :valid? false}]</code></pre><p>Now, the predicate entry is a little nicer.</p><p>Regular expressions check the content of strings.</p><pre><code>(def re #"F\dQ\d")</code><code>(defn re-pred [s] (re-matches re s))</code><br><code>(validate-scalars ["F1Q5" "F2QQ"] [re-pred re-pred])
;; => [{:datum "F1Q5",
;;      :path [0],
;;      :predicate #function [documentation/re-pred],
;;      :valid? "F1Q5"}
;;     {:datum "F2QQ",
;;      :path [1],
;;      :predicate #function [documentation/re-pred],
;;      :valid? nil}]</code></pre><p>Speculoos considers free-floating regexes in a scalar specification as predicates, so you can simply jam them in there.</p><pre><code>(valid-scalars? ["A1B2" "CDEF"] [#"(\w\d){2}" #"\w{4}"])
;; => true</code><br><code>(validate-scalars {:a "foo", :b "bar"}
                  {:a #"f.\w", :b #"^[abr]{0,3}$"})
;; => [{:datum "foo",
;;      :path [:a],
;;      :predicate #"f.\w",
;;      :valid? "foo"}
;;     {:datum "bar",
;;      :path [:b],
;;      :predicate #"^[abr]{0,3}$",
;;      :valid? "bar"}]</code></pre><p>Using bare regexes in your scalar specification has a nice side benefit in that the <code>data-from-spec</code> and <code>exercise-fn</code> utilities can generate valid strings.</p><p><code>spec.alpha</code> makes a deliberate choice to store predicates in a dedicated <a href="https://clojure.org/guides/spec#_registry">registry</a>.  Speculoos takes a more <em>laissez-faire</em> approach: specifications may live in whatever namespace you please. If you feel that some sort of registry would be useful, you could make your own <a href="https://github.com/clojure/spec.alpha/blob/c630a0b8f1f47275e1a476dcdf77507316bad5bc/src/main/clojure/clojure/spec/alpha.clj#L52">modeled after </a><code>spec.alpha</code>'s.</p><p>Finally, be aware that Speculoos presents a couple of situations where a predicate needs to accept more than one argument. First, when specifying a function's <a href="#fn-specs">arg-vs-ret</a>, the predicate must accept two inputs: the function's argument sequence and its return value. Second, when using the <a href="#path-spec"><code>validate-with-path-spec</code></a> utility, the predicate's arguments must exactly match the number of supplied paths, which could be greater than one.</p></section><section id="utility"><h2>Utility functions</h2><p>You won't miss any crucial piece of Speculoos' functionality if you don't use this namespace, but perhaps something here might make your day a little nicer. Nearly every function takes advantage of <code>speculoos.core/all-paths</code>, which decomposes a heterogeneous, arbitrarily-nested data structure into a sequence of paths and datums. With that in hand, these not-clever functions churn through the entries and give you back something useful.</p><pre><code>(require '[speculoos.utility :refer
           [scalars-without-predicates
            predicates-without-scalars thoroughly-valid?
            all-specs-okay non-predicates sore-thumb
            spec-from-data data-from-spec
            basic-collection-spec-from-data
            collections-without-predicates exercise]])</code></pre><p>Recall that Speculoos only validates a datum and predicate at identical paths in data and specification, respectively. This pair of utilities tells us where we have unmatched datums or unmatched predicates.</p><pre><code>(scalars-without-predicates [42 [:foo "abc"]] [int?])
;; => ({:path [1 0], :value :foo} {:path [1 1], :value "abc"})</code><br><code>(predicates-without-scalars [42] [int? [keyword? string?]])
;; => ({:path [1 0], :value #function [clojure.core/keyword?]}
;;     {:path [1 1],
;;      :value #function [clojure.core/string?--5475]})</code></pre><p>With this information, we can see if we were missing datums we were expecting,<label class="margin-toggle sidenote-number" for="missing"></label><input class="margin-toggle" id="missing" type="checkbox"><span class="sidenote">Or more formally, we could check for missing data by validating against a collection specification.</span></input> or we could adjust our specification for better coverage.</p><p>It's not difficult to neglect a predicate for a nested element within a collection specification, so Speculoos offers an analogous utility to highlight that possible issue.</p><pre><code>(collections-without-predicates [11 [22 {:a 33}]]
                                [vector?
                                 [{:is-a-map? map?}]])
;; => #{{:path [1], :value [22 {:a 33}]}}</code></pre><p>Yup, we didn't specify that inner vector whose first element is <code>22</code>. That's okay, though. Maybe we don't care to specify it. But now we're aware.<label class="margin-toggle sidenote-number" for="coll-preds-without"></label><input class="margin-toggle" id="coll-preds-without" type="checkbox"><span class="sidenote"><code>predicates-without-scalars</code> does not have an analogous utility for collection predicates because every predicate in a specification is contained within a collection and by policy always applies to its container. There can never be a collection predicate without a target collection.</span></input></p><p>Taking that idea to its logical conclusion, <code>thoroughly-valid?</code> returns <code>true</code> only if every scalar and every collection in data have a corresponding predicate in the scalar specification and the collection specification, respectively, and all those predicates are satisfied.</p><pre><code>;; all scalars and the vector have predicates; all predicates satisfied</code><code>(thoroughly-valid? [42 :foo 22/7]
                   [int? keyword? ratio?]
                   [vector?])
;; => true</code><br><code>;; all scalars and the vector have predicates,
;; but the 22/7 fails the scalar predicate</code><code>(thoroughly-valid? [42 :foo 22/7]
                   [int? keyword? string?]
                   [vector?])
;; => false</code><br><code>;; all scalars and the vector have predicates,
;; but the vector fails the collection predicate</code><code>(thoroughly-valid? [42 :foo 22/7]
                   [int? keyword? ratio?]
                   [list?])
;; => false</code><br><code>;; all predicates are satisfied, but the 22/7 scalar is missing a predicate</code><code>(thoroughly-valid? [42 :foo 22/7] [int? keyword?] [vector?])
;; => false</code><br><code>;; all predicates are satisfied, but the vector is missing a predicate</code><code>(thoroughly-valid? [42 :foo 22/7] [int? keyword? ratio?] [])
;; => false</code></pre><p>I can imagine passing around specifications as any other re-usable piece of data. Perhaps somewhere along the way, a wayward emacs key sequence damaged the specification. Here's a couple of functions to check the specification itself.</p><pre><code>(all-specs-okay {:a int?, :b [string?], :d [[int?]]})
;; => true</code><br><code>(non-predicates {:a int?, :b [string? "foo"], :d [[99]]})
;; => ({:path [:b 1], :value "foo"} {:path [:d 0 0], :value 99})</code></pre><p>In the first example, we learned that all our predicates are valid predicates, but in the second example, we see where our specification contains two non-predicates.</p><p>I envision that you'd be using these utility functions mainly during dev time, but I won't protest if you find them useful in production. This next utility, however, is probably only useful at the keyboard. Given data and a specification, it prints back both, but with only the invalid datums and predicates showing.</p><div class="no-display">#'documentation/sore-thumb-example#'documentation/sore-thumb-example-eval</div><pre><code>(sore-thumb [42 {:a true, :b [22/7 :foo]} 1.23]
            [int? {:a boolean?, :b [ratio? string?]} int?])</code><br><code>;; to *out*</code><code>data: [_ {:a _, :b [_ :foo]} 1.23]
spec: [_ {:a _, :b [_ #function[clojure.core/string?--5475]]} #function[clojure.core/int?]]
</code></pre><p>I've found it handy for quickly pin-pointing the unsatisfied datum-predicate pairs in a large, deeply-nested data structure.</p><p>I think of the next few utilities as <em>creative</em>, making something that didn't previously exist. We'll start with a pair of functions which perform complimentary actions.</p><pre><code>(spec-from-data [33 {:a :baz, :b [1/3 false]} '(3.14 \z)])
;; => [#function [clojure.core/int?]
;;     {:a #function [clojure.core/keyword?],
;;      :b [#function [clojure.core/ratio?]
;;          #function [clojure.core/boolean?]]}
;;     (#function [clojure.core/double?]
;;      #function [clojure.core/char?--5473])]</code><br><code>(data-from-spec
  {:x int?, :y [ratio? boolean?], :z (list char? neg-int?)}
  :random)
;; => {:x 784, :y [-1/8 true], :z (\c -5)}</code></pre><p>I hope their names give good indications of what they do. The generated specification contains only basic predicates,<label class="margin-toggle sidenote-number" for="compound"></label><input class="margin-toggle" id="compound" type="checkbox"><span class="sidenote">A few <a href="#custom-generators">paragraphs down</a> we&apos;ll see some ways to create random sample generators for compound predicates.</span></input> that is, merely <em>Is it an integer?</em>, not <em>Is it an even integer greater than 25, divisible by 3?</em>. But it's convenient raw material to start crafting a tighter specification. Oh, yeah…they both round-trip.</p><pre><code>(valid-scalars? (data-from-spec [int? keyword? string?])
                [int? keyword? string?])
;; => true</code><br><code>(valid-scalars? [42 :foo 'baz]
                (spec-from-data [42 :foo 'baz]))
;; => true</code></pre><p>Speaking of raw material, Speculoos also has a collection specification generator.</p><pre><code>(basic-collection-spec-from-data [55 {:q 33, :r ['foo 'bar]}
                                  '(22 44 66)])
;; => [{:r [#function [clojure.core/vector?--5479]],
;;      :speculoos.utility/collection-predicate
;;        #function [clojure.core/map?--5477]}
;;     (#function [clojure.core/list?])
;;     #function [clojure.core/vector?--5479]]</code></pre><p>Which does not produce a specification that is immediately useful, but does provide a good starting template, because collection specifications can be tricky to get just right.</p><p id="custom-generators">The <code>utility</code> namespace contains a trio of functions to assist writing, checking, and locating compound predicates that can be used by <code>data-from-spec</code> to generate valid random sample data. A compound predicate such as <code>#(and (int? %) (< % 100))</code> does not have built-in generator provided by <code>clojure.test.check.generators</code>. However, <code>data-from-spec</code> can extract a generator residing in the predicate's metadata.</p><pre><code>(data-from-spec [keyword?
                 (with-meta #(and (number? %) (< % 100))
                   {:speculoos/predicate->generator
                      #(rand 101)})]
                :random)
;; => [:_kcbN 35.50161649896034]</code></pre><p>The custom generator must be provided at the metadata key<code>:speculoos/predicate->generator</code>. <code>clojure.spec.alpha</code> automatically creates generators from its specs, but Speculoos isn't always that slick. Sometimes, we must write them by hand.<label class="margin-toggle sidenote-number" for="ick"></label><input class="margin-toggle" id="ick" type="checkbox"><span class="sidenote">Ugh.</span></input> It can be tricky to make sure the generator produces values that precisely satisfy the predicate, so Speculoos provides a utility to check one against the other. What if we don't quite have the generator written correctly?</p><pre><code>(require '[speculoos.utility :refer
           [validate-predicate->generator
            unfindable-generators defpred]]
         '[clojure.test.check.generators :as gen])</code><br><code>;; warm up the generator for better-looking results</code><code>(validate-predicate->generator
  (with-meta #(and (int? %) (even? %) (<= 50 500))
    {:speculoos/predicate->generator
       #(last (gen/sample (gen/such-that odd?
                                         (gen/large-integer*
                                           {:min 50,
                                            :max 500}))
                          22))})
  5)
;; => ([75 false] [103 false] [193 false] [277 false] [405 false])</code></pre><p>Oops. We paired <code>odd?</code> with <code>such-that</code> when we should have used <code>even?</code>. Let's fix it using another helper, <code>defpred</code>, that relieves us of a bit of that keyboarding.</p><pre><code>(defpred fixed
         #(and (int? %) (even? %) (<= 50 500))
         #(last (gen/sample (gen/such-that
                              even?
                              (gen/large-integer*
                                {:min 50, :max 500}))
                            22)))</code><br><code>(validate-predicate->generator fixed 5)
;; => ([498 true] [52 true] [382 true] [296 true] [324 true])</code></pre><p>We can see that the generator now yields values that satisfy its predicate.</p><p>If you don't manually supply a generator, <code>defpred</code> will create one automatically if your predicate's structure fulfills some assumptions. A top-level <code>or</code> indicates a set of possible Clojure scalar types. Let's define a predicate that tests for either an integer, keyword, or ratio and then check how it works.</p><pre><code>(defpred int-kw-ratio?
         #(or (int? %) (keyword? %) (ratio? %)))</code><br><code>(validate-predicate->generator int-kw-ratio? 9)
;; => ([4/27 true]
;;     [:fuua+ true]
;;     [19/17 true]
;;     [:.EZU9A true]
;;     [14/11 true]
;;     [-16 true]
;;     [-28 true]
;;     [-11 true]
;;     [-23/27 true])</code></pre><p><code>and</code> signals a set of modifiers as long as the first clause tests for a basic Clojure scalar type. Let's define a predicate that tests for an integer that is even and greater-or-equal to two.</p><pre><code>(defpred even-int-more-5
         (fn [i] (and (int? i) (even? i) (<= 2 i))))</code><br><code>(validate-predicate->generator even-int-more-5 5)
;; => ([10 true] [14 true] [14 true] [26 true] [2 true])</code></pre><p>Note how the first <em>s-expression</em> immediately following <code>and</code>, <code>(int? i)</code>, selects the basic Clojure scalar type. All subsequent <em>s-expressions</em> refine the integer sample generator.</p><p>Now, let's see how we might combine both <code>or</code> and <code>and</code>. We'll define a predicate that tests for either an odd integer, a string of at least three characters, or a ratio greater than one-ninth.</p><pre><code>(defpred combined-pred
         #(or (and (int? %) (odd? %))
              (and (string? %) (<= 3 (count %)))
              (and (ratio? %) (< 1/9 %))))</code><br><code>(validate-predicate->generator combined-pred 7)
;; => ([19/16 true]
;;     ["qy97T2ZKECvx0vi9Y" true]
;;     ["ynvmCob314z6Xswk2Fp3805zE" true]
;;     [13 true]
;;     [25/22 true]
;;     [11 true]
;;     [7 true])</code></pre><p>Perhaps we've got a specification in hand, and we'd like to know if all of the predicates have a random sample generator.</p><pre><code>(def not-int? #(not (int? %)))</code><code>(def needless-str? #(string? %))</code><br><code>(unfindable-generators
  [int? {:a string?, :b [not-int? needless-str?]}])
;; => [{:path [1 :b 0], :value #function [documentation/not-int?]}
;;     {:path [1 :b 1],
;;      :value #function [documentation/needless-str?]}]</code></pre><p>We see the paths to two predicates that do not have a random sample generator known to Speculoos.</p><p>One final creative utility: <code>exercise</code>, which consumes a scalar specification, and generates a series of random data, then validates them.</p><pre><code>(exercise [int? symbol? {:x boolean?, :y ratio?}] 5)
;; => ([[247 G:x:_O-/*fv {:x false, :y -3/8}] true]
;;     [[-325 N0M_/YG8I!Q {:x true, :y -1/2}] true]
;;     [[633 qqSg_/! {:x false, :y 21/5}] true]
;;     [[-321 j_-1!yy5/UUd5 {:x true, :y -7/13}] true]
;;     [[-547 I+_X/G5 {:x true, :y -5/6}] true])</code></pre><p>I'm not sure that's generally useful, but I include it to illustrate the benefits of the <code>all-paths</code> structure. All these utilities contain just a handful of lines because <code>all-paths</code> has already done the hard work, which makes me feel like Speculoos somehow aligns with the spirit of Alan Perlis' <a href="https://web.archive.org/web/19990117034445/http://www-pu.informatik.uni-tuebingen.de/users/klaeren/epigrams.html">epigram #9</a>.</p><p>Now that we've finished the creative utilities, the next involve altering data structures and specifications. I do not know if any of these would be useful in the real world, but they were quick to write, and again illustrate the power of the <code>all-paths</code> core function.</p><p>Imagine a specification damaged by cosmic rays, and some data that we know for certain is valid. Speculoos can mangle the specification into working.<label class="margin-toggle sidenote-number" for="immutable"></label><input class="margin-toggle" id="immutable" type="checkbox"><span class="sidenote"><em>Mangle</em> only in the everyday sense. All the Clojure data structures — including Speculoos specifications — remain immutable.</span></input> If for some reason our specification contained an entry that was not a predicate, Speculoos could swap it out so that the specification can be fed into <code>validate</code>.</p><pre><code>(require '[speculoos.utility :refer
           [swap-non-predicates nil-out bed-of-procrustes
            apathetic bazooka-swatting-flies smash-data]])</code><br><code>(swap-non-predicates [int? :foo string?])
;; => [#function [clojure.core/int?] #function [clojure.core/any?]
;;     #function [clojure.core/string?--5475]]</code></pre><p>Similarly, if we just couldn't tolerate invalid datum-predicate pairs, we could replace each with <code>nil/nil?</code>.</p><pre><code>(nil-out [42 'foo 22/7 true] [int? keyword? ratio? float?])
;; => {:data [42 nil 22/7 nil],
;;     :spec [#function [clojure.core/int?]
;;            #function [clojure.core/nil?]
;;            #function [clojure.core/ratio?]
;;            #function [clojure.core/nil?]]}</code></pre><p>Starting to get ridiculous, but if we wanted to completely remove invalid datum-predicate pairs, we have this…</p><pre><code>(bed-of-procrustes [42 {:a true, :b 1.23} :foo]
                   [int? {:a boolean?, :b int?} symbol?])
;; => {:data [42 {:a true}],
;;     :spec [#function [clojure.core/int?]
;;            {:a #function [clojure.core/boolean?]}]}</code></pre><p>If you have some data you really like, but your specification, not so much, you could mangle the specification into compliance.</p><pre><code>(apathetic [42 'foo {:x 22/7, :y \z}]
           [int? keyword? {:x ratio?, :y string?}])
;; => [#function [clojure.core/int?] #function [clojure.core/any?]
;;     {:x #function [clojure.core/ratio?],
;;      :y #function [clojure.core/any?]}]</code></pre><p>Along those same lines, if you have some good data, and you want to conform your specification to it with predicates that validate, Speculoos gives you this.</p><pre><code>(bazooka-swatting-flies [42 'foo {:x 22/7, :y \z}]
                        [int? keyword?
                         {:x ratio?, :y string?}])
;; => [#function [clojure.core/int?]
;;     #function [clojure.core/symbol?]
;;     {:x #function [clojure.core/ratio?],
;;      :y #function [clojure.core/char?--5473]}]</code></pre><p>On the other hand, if you have a specification that you like, and some data that only kinda works, Speculoos can force the square data peg into the round hole.</p><pre><code>(smash-data [42 'foo {:x 22/7, :y \z}]
            [int? keyword? {:x ratio?, :y string?}])
;; => [42 :kw {:x 22/7, :y "abc"}]</code></pre><p>I don't advocate using any of these mangling utilities except as a means to temporarily get something done during development.</p></section><section id="fn-specs"><h2>Functions specification and testing</h2><p>Being able to validate Clojure data enables us to inspect functions. Function arguments take the form of a sequence, which Speculoos can validate like any other heterogeneous, arbitrarily-nested data structure. Likewise, the function return is a value, which may be also validated. Furthermore, <code>spec.alpha</code> can verify a specified relationship between a function's arguments and its return value. Speculoos supports that idea, too.  Speculoos inspects functions at three sites: validating arguments, validating returns, and validating the relationships between arguments and returns. Within each of those three sites, Speculoos offers both scalar validation and collection validation, for a total of six possible validations. None are strictly required. Speculoos will happily validate only what you ask.</p><p>Speculoos function specifications <a href="https://clojure.org/about/spec#_dont_further_add_tooverload_the_reified_namespaces_of_clojure">differ</a> from <code>spec.alpha</code> in that they are stored and retrieved directly from the function's metadata. It's an experiment, but I thought it might be rather nice if I could hand you one single thing and say </p><blockquote><p><em>Here's a Clojure function you can use. Its name suggests what it does, its docstring that tells you how to use it, and human- and machine-readable specifications  check the validity of the inputs, and tests that it's working properly. All in one neat, tidy </em>S-expression<em>.</em></p></blockquote><p>Speculoos offers three patterns of function validation.<ol><li>Explicit validation with a specification supplied in a separate map. The function var is not altered.</li><li>Explicit validation with specifications contained in the function's metadata.</li><li>Implicit validation with specifications contained in the function's metadata.</li></ol></p><p>The first pattern is nice because you can quickly validate a function <em>on-the-fly</em> without messing with the function's metadata. Merely supply the function's symbol, a map of specifications, and a sequence of args.</p><pre><code>(require '[speculoos.function-specs :refer
           [validate-fn-with validate-fn-meta-spec
            inject-specs! unject-specs! instrument
            unstrument exercise-fn recognized-spec-keys
            validate-higher-order-fn]])</code><br><code>(defn add-ten
  "Returns ten more than integer i."
  [i]
  (+ 10 i))</code><br><code>(validate-fn-with add-ten
                  {:speculoos/arg-scalar-spec [int?],
                   :speculoos/ret-scalar-spec int?}
                  15)
;; => 25</code></pre><p>Since all specifications are satisfied, <code>add-ten</code> returns its evaluation. When one or more predicates are not satisfied, Speculoos returns a validation report.</p><pre><code>(validate-fn-with add-ten
                  {:speculoos/arg-scalar-spec [float?],
                   :speculoos/ret-scalar-spec float?}
                  15)
;; => ({:datum 15,
;;      :fn-spec-type :speculoos/argument,
;;      :path [0],
;;      :predicate #function [clojure.core/float?],
;;      :valid? false}
;;     {:datum 25,
;;      :fn-spec-type :speculoos/return,
;;      :path nil,
;;      :predicate #function [clojure.core/float?],
;;      :valid? false})</code></pre><p>Speculoos steps in to tell us that neither the argument nor the return value are floating point numbers. Note that in this example, the return value is a bare scalar. When a function returns a non-collection, Speculoos handles it.</p><p>Speculoos consults the following defined group of keys in a specification map when it validates.</p><pre><code>speculoos.function-specs/recognized-spec-keys
;; => [:speculoos/arg-scalar-spec :speculoos/arg-collection-spec
;;     :speculoos/ret-scalar-spec :speculoos/ret-collection-spec
;;     :speculoos/arg-vs-ret-scalar-spec
;;     :speculoos/arg-vs-ret-collection-spec
;;     :speculoos/canonical-sample :speculoos/predicate->generator
;;     :speculoos/hof-specs]</code></pre><p>Speculoos offers a pair convenience functions to add and remove specifications from a function's metadata.</p><pre><code>(inject-specs! add-ten
               {:speculoos/arg-scalar-spec [int?],
                :speculoos/ret-scalar-spec int?})
;; => nil</code></pre><p>We can observe that the specifications indeed live in the function's metadata. If we later decided to undo that, <code>unject-specs!</code> removes all recognized Speculoos specification entries, regardless of how they got there. For the upcoming demonstrations, though, we'll keep those specifications in <code>add-ten</code>'s metadata.</p><pre><code>(select-keys (meta #'add-ten) recognized-spec-keys)
;; => #:speculoos{:arg-scalar-spec [#function
;;                                   [clojure.core/int?]],
;;                :ret-scalar-spec #function [clojure.core/int?]}</code></pre><p>Now that <code>add-ten</code> holds the specifications in its metadata, we can try the second pattern of explicit validation pattern. It's similar, except we don't have to supply the specification map; it's already waiting in the metadata. Invoked with a valid argument, <code>add-ten</code> returns a valid value.</p><pre><code>(validate-fn-meta-spec add-ten 15) ;; => 25</code></pre><p>Invoking <code>add-ten</code> with an invalid float, Speculoos generates a report.</p><pre><code>(validate-fn-meta-spec add-ten 1.23)
;; => ({:datum 1.23,
;;      :fn-spec-type :speculoos/argument,
;;      :path [0],
;;      :predicate #function [clojure.core/int?],
;;      :valid? false}
;;     {:datum 11.23,
;;      :fn-spec-type :speculoos/return,
;;      :path nil,
;;      :predicate #function [clojure.core/int?],
;;      :valid? false})</code></pre><p>Until this point in our discussion, Speculoos has only performed function validation when we explicitly call either <code>validate-fn-with</code> or <code> validate-fn-meta-spec</code>. The specifications in the metadata are passive and produce no effect, even with arguments that would otherwise fail to satisfy the specification's predicates.</p><pre><code>(add-ten 15) ;; => 25</code><br><code>(add-ten 1.23) ;; => 11.23</code></pre><p>The arguments list to a function is exactly that: a sequence. And Speculoos can validate sequences, both its scalars and as a collection. Let's see a multi-argument function that returns a collection. We create a named collection specification, then define our silly function.</p><pre><code>(defn length-3? [v] (= 3 (count v)))</code><code>(defn silly
  "A contrived demo."
  [x s r]
  (vector (/ r 2)
          (inc x)
          (apply str (reverse (.toString s)))))</code></pre><p>Next, we inject our specifications into the function's metadata: a scalar specification and collection specification, each, for both the argument sequence and the return sequence. A grand total of four specifications.</p><pre><code>(inject-specs!
  silly
  {:speculoos/arg-scalar-spec [int? string? ratio?],
   :speculoos/arg-collection-spec [length-3?],
   :speculoos/ret-scalar-spec [ratio? int? string?],
   :speculoos/ret-collection-spec [length-3?]})
;; => nil</code></pre><p>Valid inputs…</p><pre><code>(validate-fn-meta-spec silly 42 "abc" 1/3)
;; => [1/6 43 "cba"]</code></pre><p>…produce valid returns. But invalid arguments…</p><pre><code>(validate-fn-meta-spec silly 42 \a 9)
;; => ({:datum \a,
;;      :fn-spec-type :speculoos/argument,
;;      :path [1],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? false}
;;     {:datum 9,
;;      :fn-spec-type :speculoos/argument,
;;      :path [2],
;;      :predicate #function [clojure.core/ratio?],
;;      :valid? false})</code></pre><p>…yields an invalidation report: two of our arguments do not satisfy their scalar predicates.</p><p>Speculoos' third pattern of function validation <em>instruments</em> the function using the metadata specifications.<label class="margin-toggle sidenote-number" for="fagile"></label><input class="margin-toggle" id="fragile" type="checkbox"><span class="sidenote"><code>instrument</code>-style function validation is very much a work in progress. The implementation is sensitive to invocation order and can choke on multiple calls. The var mutation is not robust. Beware.</span></input> Every invocation of the function itself automatically validates any specified arguments and return values.</p><pre><code>(instrument silly)</code><br><code>(silly 42 "abc" 1/3) ;; => [1/6 43 "cba"]</code><br><code>(silly 42 \a 9) ;; => [9/2 43 "a"]</code><br><code>(with-out-str (silly 42 \a 9))<br>;; => ({:path [1], :datum a, :predicate #function[clojure.core/string?--5475], :valid? false}
;; =>  {:path [2], :datum 9, :predicate #function[clojure.core/ratio?], :valid? false})</code></pre><p>The function returns if it doesn't throw an exception. Any non-satisfied predicates are reported to <code>*out*</code>. When we are done, we can <em>unstrument</em> the function, and Speculoos will no longer intervene.</p><pre><code>(unstrument silly)</code><br><code>(silly 42 \a 9) ;; => [9/2 43 "a"]</code></pre><p>Even though character <code>\a</code> and non-ratio <code>9</code> do not satisfy the specifications, <code>silly</code> is no longer instrumented, and Speculoos is not intercepting its invocation, so the function returns a value.<label class="margin-toggle sidenote-number" for="preference"></label><input class="margin-toggle" id="preference " type="checkbox"><span class="sidenote">Frankly, I wrote <code>instrument/unstrument</code> to mimic the features <code>spec.alpha</code> offers. My implementation is squirrelly, and I don&apos;t like mutating vars. I lean much more towards the deterministic <code>validate-fn-meta-spec</code> and <code>validate-fn-with</code>.</span></input></p><p>Beyond validating a function's argument sequence and its return, Speculoos can perform a validation that checks the relationship between any aspect of the arguments and return values. When the arguments sequence and return sequence share a high degree of shape, an <em>argument versus return scalar specification</em> will work well. A good example of this is using <code>map</code> to transform a sequence of values. Each item in the return sequence has a corresponding item in the argument sequence.</p><pre><code>(defn mult-ten [& args] (map #(* 10 %) args))</code><br><code>(mult-ten 1 2 3) ;; => (10 20 30)</code></pre><p>The predicates in a <em>versus</em> specification are a little bit unusual. Each predicate accepts two arguments: the first is the element from the argument sequence, the second is the corresponding element from the return sequence.</p><pre><code>(defn ten-times? [a r] (= (* 10 a) r))</code></pre><p>Let's make a bogus predicate that we know will fail, just so we can see what happens when a relationship is not satisfied.</p><pre><code>(defn nine-times? [a r] (= (* 9 a) r))</code></pre><p>And stuff them into a map whose keys Speculoos recognizes.</p><pre><code>(def mult-ten-vs-spec
  {:speculoos/arg-vs-ret-scalar-spec [ten-times? nine-times?
                                      ten-times?]})</code></pre><p>Now that we've prepared our predicates and composed a versus specification, we can validate the relationship between the arguments and the returns.</p><pre><code>(validate-fn-with mult-ten mult-ten-vs-spec 1 2 3)
;; => ({:datum-1 2,
;;      :datum-2 20,
;;      :fn-spec-type :speculoos/arg-vs-ret,
;;      :path [1],
;;      :predicate #function [documentation/nine-times?],
;;      :valid? false})</code></pre><p>We intentionally constructed our specification to fail at the middle element, and sure enough, the validation report tells us the argument and return scalars do not share the declared relationship.</p><p>To complete the <em>scalars/collections/arguments/returns/versus</em> feature matrix, Speculoos can also validate function argument collections against return collections. All of the previous discussion holds, with the twist that the specification predicates apply against the argument collections and return collections. Examples show better than words.</p><pre><code>(defn goofy-reverse [& args] (reverse args))</code><br><code>(goofy-reverse 1 2 3) ;; => (3 2 1)</code></pre><p> Speculoos passes through the function's return if all predicates are satisfied, so we'll intentionally bungle one of the predicates to cause an invalidation report.</p><pre><code>(defn equal-lengths?
  "Buggy!"
  [v1 v2]
  (= (+ 1 (count v1)) (count v2)))</code><br><code>(defn mirror-elements-equal?
  [v1 v2]
  (= (first v1) (last v2)))</code></pre><p>Composing our args versus return collection specification, using the properly pseudo-qualified key.</p><pre><code>(def rev-coll-spec
  {:speculoos/arg-vs-ret-collection-spec
     [equal-lengths? mirror-elements-equal?]})</code></pre><p>Our goofy reverse function fails our buggy argument versus return validation.</p><pre><code>(validate-fn-with goofy-reverse rev-coll-spec 1 2 3)
;; => ({:datum-1 [1 2 3],
;;      :datum-2 (3 2 1),
;;      :fn-spec-type :speculoos/arg-vs-ret,
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :predicate #function [documentation/equal-lengths?],
;;      :valid? false,
;;      :value #function [documentation/equal-lengths?]})</code></pre><p><code>goofy-reverse</code> behaves exactly as it should, but for illustration purposes, we applied a buggy collection specification that we knew would fail. The validation report shows us the two things it compared, in this instance, the argument sequence and the returned, reversed sequence, and furthermore, that those two collections failed to satisfy the buggy predicate, <code>equal-lengths?</code>. The other predicate, <code>mirror-elements-equal?</code> was satisfied because the first element of the argument collection is equal to the last element of the return collection, and was therefore not included in the report.<p>Speculoos has a story about validating higher-order functions, too. It uses very similar patterns to regular function validation: put some specifications in the function's metadata with the properly qualified keys, then invoke the function with some sample arguments, then Speculoos will validate the results. Here's how it works. A flourish of the classic adder <span class="small-caps">hof</span>.</p></p><pre><code>(defn addder [x] (fn [y] (fn [z] (+ x (* y z)))))</code><br><code>(((addder 3) 2) 10) ;; => 23</code></pre><p><code>addder</code> returns a function upon it first two invocations, and only on its third does it return a scalar. Specifying and validating a function value does not convey much meaning,<label class="margin-toggle sidenote-number" for="fn-value?"></label><input class="margin-toggle" id="fn-value?" type="checkbox"><span class="sidenote">It would merely satisfy <code>fn?</code> which isn&apos;t very interesting.</span></input> so to validate a <span class="small-caps">hof</span>, Speculoos requires it to be invoked until it produces a value. So we'll supply the validator a series of <code>&-args</code> that, when fed in order to the <span class="small-caps">hof</span>, will produce a result. For the example above, it will look like <code>[3] [2] [10]</code>.</p><p>The last task we must do is create the specification. <span class="small-caps">hof</span> specifications live in the function's metadata at key <code>:speculoos/hof-specs</code> which is a series of nested specification maps, one nesting for each returned function. For this example, we might create this <span class="small-caps">hof</span> specification.</p><pre><code>(def addder-spec
  {:speculoos/arg-scalar-spec [even?],
   :speculoos/hof-specs
     {:speculoos/arg-scalar-spec [ratio?],
      :speculoos/hof-specs {:speculoos/arg-scalar-spec
                              [float?]}}})</code></pre><p>Once again, for illustration purposes, we've crafted predicates that we know will invalidate, but will permit the function stack to evaluate to completion.<label class="margin-toggle sidenote-number" for="halt"></label><input class="margin-toggle" id="halt" type="checkbox"><span class="sidenote">Validation halts on exceptions.</span></input></p><p><span class="small-caps">hof</span> validation requires that the metadata hold the specifications. So we inject them.</p><pre><code>(inject-specs! addder addder-spec) ;; => nil</code></pre><p>And finally execute the validation.</p><pre><code>(validate-higher-order-fn addder [3] [5] [10])
;; => ({:datum 3,
;;      :fn-tier :speculoos/argument,
;;      :path [0 0],
;;      :predicate #function [clojure.core/even?],
;;      :valid? false}
;;     {:datum 5,
;;      :fn-tier :speculoos/argument,
;;      :path [1 0],
;;      :predicate #function [clojure.core/ratio?],
;;      :valid? false}
;;     {:datum 10,
;;      :fn-tier :speculoos/argument,
;;      :path [2 0],
;;      :predicate #function [clojure.core/float?],
;;      :valid? false})</code></pre><p>Let's step through the validation results. Speculoos validates <code>3</code> against predicate <code>even?</code> and then invokes <code>addder</code> with argument <code>3</code>. It then validates <code>5</code> against predicate <code>ratio?</code> and then invokes the returned function with argument <code>5</code>. Finally, Speculoos validates <code>10</code> against predicate <code>float?</code> and invokes the previously returned function with argument <code>10</code>. If all the predicates were satisfied, Speculoos would yield the return value of the function call. In this case, all three arguments are invalid, and Speculoos yields a validation report.</p><p>Another tool Speculoos offers in this category: exercising specified functions. If you have injected argument scalar specifications into your function, Speculoos can generate a series of specification-satisfying arguments and invoke your function. Let's take advantage of the nice feature of <code>defn</code> that adds metadata during function definition.</p><pre><code>(defn bottles
  {:speculoos/arg-scalar-spec [pos-int? string?]}
  [n liquid]
  (str n
       " bottles of "
       liquid
       " on the wall, "
       n
       " bottles of "
       liquid
       "..."))</code><br><code>(bottles 99 "espresso")
;; => "99 bottles of espresso on the wall, 99 bottles of espresso..."</code></pre><p>Now, Speculoos can exercise our function.</p><pre><code>(exercise-fn bottles 5)
;; => ([[1 "97v9I8i"]
;;      "1 bottles of 97v9I8i on the wall, 1 bottles of 97v9I8i..."]
;;     [[19 "849v4JVyN6pWzYorz5hr4bqHJyXqX9"]
;;      "19 bottles of 849v4JVyN6pWzYorz5hr4bqHJyXqX9 on the wall, 19 bottles of 849v4JVyN6pWzYorz5hr4bqHJyXqX9..."]
;;     [[15 "1k25cva5GX7UJPM9q0d123T4"]
;;      "15 bottles of 1k25cva5GX7UJPM9q0d123T4 on the wall, 15 bottles of 1k25cva5GX7UJPM9q0d123T4..."]
;;     [[4 "E5N5i862"]
;;      "4 bottles of E5N5i862 on the wall, 4 bottles of E5N5i862..."]
;;     [[6 "O2oA1b5HZ4MeYICyd"]
;;      "6 bottles of O2oA1b5HZ4MeYICyd on the wall, 6 bottles of O2oA1b5HZ4MeYICyd..."])</code></pre><p>Not exactly thirst-quenching.</p><p>Finally, Speculoos can validate the macroexpansion of a macro against a scalar specification. Let's write a dinky example macro.</p><pre><code>(require '[speculoos.core :refer
           [validate-macro-with valid-macro?]])</code><br><code>(defmacro dinky-macro [f x] `(~f ~@x))</code><br><code>(macroexpand-1 '(dinky-macro + [1 2 3])) ;; => (+ 1 2 3)</code></pre><p>We must remember that things that look like functions in a macroexpansion are actually symbols. Let's write a scalar specification, then validate the macroexpansion.</p><pre><code>(def dinky-specification (list symbol? int? int? int?))</code><br><code>(validate-macro-with '(dinky-macro + [1 2 3]) dinky-specification)
;; => [{:path [0], :datum +, :predicate clojure.core/symbol?, :valid? true}
;;     {:path [1], :datum 1, :predicate clojure.core/int?, :valid? true}
;;     {:path [2], :datum 2, :predicate clojure.core/int?, :valid? true}
;;     {:path [3], :datum 3, :predicate clojure.core/int?, :valid? true}]</code></pre><p>Or, more briefly.</p><pre><code>(valid-macro? '(dinky-macro + [1 2 3]) dinky-specification)
;; => true</code></pre><div class="no-display">Macro validation is included in the <code>core</code> namespace and not the <code>function-specs</code> namespace because Speculoos validates macroexpansion on a structural basis, not it arguments nor returns.</div></section><section id="non-terminating"><h2>Non-terminating sequences</h2><p>Speculoos absorbs lots of power from Clojure's infinite, lazy sequences.<label class="margin-toggle sidenote-number" for="inifinite"></label><input class="margin-toggle" id="infinite" type="checkbox"><span class="sidenote"><em>(possibly) non-terminating sequences</em> might be more accurate.</span></input> That power stems from the fact that Speculoos only validates complete pairs of datums and predicates. Datums without predicates are not validated, and predicates without datums are ignored. That policy provides optionality in your data. If a datum is present, it is validated against its corresponding predicate, but if that datum is non-existent, it is not required.</p><pre><code>(valid-scalars? [42] [int? keyword? char?]) ;; => true</code><br><code>(valid-scalars? [42 :foo \z] [int?]) ;; => true</code></pre><p>In the first example, only the single integer is validated, the rest of the predicates are ignored. In the second example, only the first integer was validated because the specification implies that any trailing elements are un-specified. We can take advantage of this fact by intentionally making either the data or the specification <em>run off the end</em>.</p><p>First, if you'd like to validate a non-terminating sequence, specify as many datums as necessary to capture the pattern. <code>repeat</code> produces multiple instances of a single value, so we only need to specify one datum.</p><pre><code>(valid-scalars? (repeat 3) [int?]) ;; => true</code></pre><p><code>cycle</code> can produce different values, so we ought to test for as many as appear in the definition.</p><pre><code>(valid-scalars? (cycle [42 :foo 22/7])
                [int? keyword? ratio?])
;; => true</code></pre><p>Three unique datums. Only three predicates needed.</p><p>On the other side of the coin, non-terminating sequences serve a critical role in composing Speculoos specifications. They express <em>I don't know how many items there are in this sequence, but they all must satisfy these predicates</em>.</p><pre><code>(valid-scalars? [1] (repeat int?)) ;; => true</code><code>(valid-scalars? [1 2] (repeat int?)) ;; => true</code><code>(valid-scalars? [1 2 3] (repeat int?)) ;; => true</code><code>(valid-scalars? [1 2 3 4] (repeat int?)) ;; => true</code><code>(valid-scalars? [1 2 3 4 5] (repeat int?)) ;; => true</code></pre><p>Basically, it serves the role of a regular expression <code>zero-or-more</code>. Let's pretend we'd like to validate an integer, then a string, followed by any number of characters. We compose our specification like this.</p><pre><code>(valid-scalars? [99 "abc" \x \y \z]
                (concat [int? string?] (repeat char?)))
;; => true</code><br><code>(only-invalid (validate-scalars [99 "abc" \x "y" \z]
                                (concat [int? string?]
                                        (repeat char?))))
;; => ({:datum "y",
;;      :path [3],
;;      :predicate #function [clojure.core/char?--5473],
;;      :valid? false})</code></pre><p>Or perhaps we'd like to validate a function's argument list composed of a ratio followed by <code>&-args</code> consisting of any number of alternating keyword-string pairs.</p><pre><code>(valid-scalars? [2/3]
                (concat [ratio?] (cycle [keyword string?])))
;; => true</code><br><code>(valid-scalars? [2/3 :opt1 "abc" :opt2 "xyz"]
                (concat [ratio?] (cycle [keyword string?])))
;; => true</code><br><code>(only-invalid (validate-scalars [2/3 :opt1 'foo]
                                (concat [ratio?]
                                        (cycle [keyword
                                                string?]))))
;; => ({:datum foo,
;;      :path [2],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? false})</code></pre><p>Using non-terminating sequences this way sorta replicates <code>spec.alpha</code>'s sequence regexes. I think of it as Speculoos' super-power.</p><p>Also, Speculoos can handle nested, non-terminating sequences.</p><pre><code>(valid-scalars? [[1] [2 "2"] [3 "3" :3]]
                (repeat (cycle [int? string? keyword?])))
;; => true</code></pre><p>One implementation detail: A non-terminating sequence must not appear at the same path within both the data and specification. I am not aware of any method to inspect a sequence to determine if it is infinite, so Speculoos will refuse to validate a non-terminating data sequence at the same path as a non-terminating predicate sequence, and <em>vice versa</em>. However, feel free to use them in either data or in the specification, as long as they live at different paths.</p><pre><code>(valid-scalars? {:a (repeat 42), :b [22/7 true]}
                {:a [int?], :b (cycle [ratio? boolean?])})
;; => true</code><br><code>(only-invalid (validate-scalars {:a (repeat 42),
                                 :b [22/7 true]}
                                {:a [int? int? string?],
                                 :b (repeat ratio?)}))
;; => ({:datum 42,
;;      :path [:a 2],
;;      :predicate #function [clojure.core/string?--5475],
;;      :valid? false}
;;     {:datum true,
;;      :path [:b 1],
;;      :predicate #function [clojure.core/ratio?],
;;      :valid? false})</code></pre><p>Here, the data contains a non-terminating sequence at key <code>:a</code>, while the specification contains a non-terminating sequence at key <code>:b</code>. Since the two do not share a path, validation can proceed to completion.</p><p>So what's going on? Internally, Speculoos finds all the potentially non-terminating sequences in both the data and the specification. For each of those hits, Speculoos looks into the other nested structure to determine how long the counterpart sequence is. Speculoos then <em>clamps</em> the non-terminating sequence to that length. Validation proceeds with the clamped sequences. Let's see the clamping in action.</p><pre><code>(require '[speculoos.core :refer [expand-and-clamp-1]])</code><br><code>(expand-and-clamp-1 (range) [int? int? int?]) ;; => [0 1 2]</code></pre><p><code>range</code> would have continued merrily on forever, but the clamp truncated it at three elements, the length of the second argument vector. That's why two non-terminating sequences at the same path are not permitted. Speculoos has no way of knowing how short or long the sequences ought to be, so instead of making a bad guess, it throws the issue back to you.</p><p>Speculoos' <a href="#utility"><code>utility</code></a> namespace provides a <code>clamp-in*</code> tool for you to clamp any sequence within a homogeneous, arbitrarily-nested data structure. You invoke it with the same pattern of arguments as <code>assoc-in*</code> which, in turn, mimics <code>clojure.core/assoc-in</code>.</p><pre><code>(require '[speculoos.utility :refer [clamp-in*]])</code><br><code>(clamp-in* {:a 42, :b ['foo 22/7 {:c (cycle [3 2 1])}]}
           [:b 2 :c]
           5)
;; => {:a 42, :b [foo 22/7 {:c [3 2 1 3 2]}]}</code></pre><p><code>clamp-in*</code> used the path <code>[:b 2 :c]</code> to locate the non-terminating sequence, clamped it to five elements, and returned the new data structure with that terminating sequence. This way, if Speculoos squawks at you, you have a way to clamp the data, specification, or both at any path, and validation can proceed.</p><p>Be sure to set your development environment's printing length</p><pre><code>(set! *print-length* 99) ;; => 99</code></pre><p>or you may jam up your session.</p></section><section id="sets"><h2>Sets</h2><p>Sets are…special. They enable some nice features, but they present some unique challenges compared to the other Clojure collections. <em>The elements in a set are addressed by their identities.</em> What does that even mean? Let's compare to Clojure's other collections to get some context.</p><p>The elements of a sequence are addressed by monotonically increasing integer indexes. Give a vector index <code>2</code> and it'll give you back the third element, if it exists.</p><pre><code>([11 22 33] 2) ;; => 33</code></pre><p>The elements of a map are addressed by its keys. Give a map a key <code>:howdy</code> and it'll give you back the value at that key, if it exists.</p><pre><code>({:hey "salut", :howdy "bonjour"} :howdy) ;; => "bonjour"</code></pre><p>Give a set some value, and it will give you back that value…</p><pre><code>(#{:index :middle :pinky :ring :thumb} :thumb) ;; => :thumb</code></pre><p>…but only if that element exists in the set.</p><pre><code>(#{:index :middle :pinky :ring :thumb} :bird) ;; => nil</code></pre><p>So the <a href="#path">paths</a> to elements of vectors, lists, and maps are composed of indexes or keys. The paths to members of a set are the thing themselves. Let's take a look at a couple of examples.</p><pre><code>(all-paths #{:foo 42 "abc"})
;; => [{:path [], :value #{42 :foo "abc"}}
;;     {:path ["abc"], :value "abc"} {:path [:foo], :value :foo}
;;     {:path [42], :value 42}]</code></pre><p>In the first example, the root element, a set, has a path <code>[]</code>. The remaining three elements, direct descendants of the root set have paths that consist of themselves. We find <code>42</code> at path <code>[42]</code> and so on. The second example applies the principle further.</p><pre><code>(all-paths #{11 {:a [22 #{33}]}})
;; => [{:path [], :value #{11 {:a [22 #{33}]}}}
;;     {:path [{:a [22 #{33}]}], :value {:a [22 #{33}]}}
;;     {:path [{:a [22 #{33}]} :a], :value [22 #{33}]}
;;     {:path [{:a [22 #{33}]} :a 0], :value 22}
;;     {:path [{:a [22 #{33}]} :a 1], :value #{33}}
;;     {:path [{:a [22 #{33}]} :a 1 33], :value 33}
;;     {:path [11], :value 11}]</code></pre><p>How would we navigate to that <code>33</code>? Again the root element set has a path <code>[]</code>. There are two direct descendants of the root set: <code>11</code> and a map. We've already seen that the integer's path is the value of the integer. The path to the map is the map itself, which appears as the first element of its path. That path may look unusual, but Speculoos <a href="#fn-in*">starred functions</a> take it without skipping a beat.</p><pre><code>(get-in* #{11 {:a [22 #{33}]}} [{:a [22 #{33}]}])
;; => {:a [22 #{33}]}</code></pre><p>The map has one <code>MapEntry</code>, key <code>:a</code>, with an associated value, a two-element vector <code>[22 #{33}]</code>. A map value is addressed by its key, so the vector's path contains that key. Its path is that of its parent, with its key appended.</p><pre><code>(get-in* #{11 {:a [22 #{33}]}} [{:a [22 #{33}]} :a])
;; => [22 #{33}]</code></pre><p>Paths into a vector are old hat by now. Our <code>33</code> is in a set at the second position, index <code>1</code> in zero-based land, which we append to the path.</p><pre><code>(get-in* #{11 {:a [22 #{33}]}} [{:a [22 #{33}]} :a 1])
;; => #{33}</code></pre><p>We've now arrived at the little nested set which holds our <code>33</code>. Items in a set are addressed by their identity, and the identity of <code>33</code> is <code>33</code>. So we append that to the path so far.</p><pre><code>(get-in* #{11 {:a [22 #{33}]}} [{:a [22 #{33}]} :a 1 33])
;; => 33</code></pre><p>And now we've got our goal. Following this algorithm, we can get, change, and delete any element of any heterogeneous, arbitrarily-nested data structure, and that includes sets at any level of nesting. We could even make a path to a set, nested within a set, nested within a set, but it's not pretty. Best to let Speculoos handle it.</p><p>Speculoos can validate scalars within a set during a scalar validation operation. Validating a set's scalar members follows all the same principles as validating a vector's scalar members, except for one wrinkle: Since elements of a set have no inherent location<label class="margin-toggle sidenote-number" for="unordered"></label><input class="margin-toggle" id="unordered" type="checkbox"><span class="sidenote">I.e., sets are unordered.</span></input> sets in our data are validated against all predicates contained in the corresponding set at the same path in the specification. An example shows this better than words.</p><pre><code>(valid-scalars? [42 #{:glass :rubber :paper}]
                [int? #{keyword?}])
;; => true</code></pre><p>Speculoos validates <code>42</code> against predicate <code>int?</code> because they share paths in their respective vectors. At vector index <code>1</code> our data and specification both hold sets, so Speculoos enters <em>set-scalar-validation-mode</em><label class="margin-toggle sidenote-number" for="mode"></label><input class="margin-toggle" id="mode" type="checkbox"><span class="sidenote"><em>Mode</em> only in the casual sense.  There&apos;s no modes nor states. The algorithm merely branches to treat the set differently depending on the situation.</span></input>. Every predicate contained in the specification set is applied to every datum in the data's set. In this example, <code>keyword?</code> is individually applied to <code>:glass</code>, <code>:rubber</code>, and <code>:paper</code>, and since each satisfy the predicate, the validation returns <code>true</code>.</p><p>If our specification set contains more than one predicate, each of the predicates is applied to all the scalars in the data's set. In the next example, the specification set contains two predicates.</p><pre><code>(validate-scalars #{:chocolate :vanilla :strawberry}
                  #{keyword? qualified-keyword?})
;; => ({:datum #{:chocolate :strawberry :vanilla},
;;      :path {:path [],
;;             :value #{:chocolate :strawberry :vanilla}},
;;      :predicate #function [clojure.core/keyword?],
;;      :valid? true}
;;     {:datum #{:chocolate :strawberry :vanilla},
;;      :path {:path [],
;;             :value #{:chocolate :strawberry :vanilla}},
;;      :predicate #function [clojure.core/qualified-keyword?],
;;      :valid? false})</code></pre><p>Validation applies <code>keyword?</code> and <code>qualified-keyword?</code>, in turn, to every scalar member of the data set. Speculoos tells us that all the scalars in the data are indeed keywords, but at least one of the data's scalars is not a qualified keyword. Notice how Speculoos condenses the validation results. Instead of a validation entry for each individual scalar in the data set, Speculoos combines all the results for all the scalars. Two predicates, two validation results.</p><p>Clojure sets can serve as membership tests.</p><pre><code>(def self? #{:me :myself :I})</code><br><code>(ifn? self?) ;; => true</code><br><code>(self? :me) ;; => :me</code><code>(self? :you) ;; => nil</code></pre><p><code>self?</code> implements <code>IFn</code> and thus behaves like a predicate when invoked as a function. <code>:me</code> satisfies our <code>self?</code> predicate, whereas <code>:you</code> does not.</p><p>When a scalar in our data set shares a path with a set in the specification, Speculoos enters <em>set-as-a-predicate</em> mode.</p><pre><code>(def geography #{:mountain :plateau :valley})</code><br><code>(validate-scalars [:valley {:a :mountain, :b :river}]
                  [geography {:a geography, :b geography}])
;; => [{:datum :valley,
;;      :path [0],
;;      :predicate #{:mountain :plateau :valley},
;;      :valid? :valley}
;;     {:datum :mountain,
;;      :path [1 :a],
;;      :predicate #{:mountain :plateau :valley},
;;      :valid? :mountain}
;;     {:datum :river,
;;      :path [1 :b],
;;      :predicate #{:mountain :plateau :valley},
;;      :valid? nil}]</code></pre><p>Our example data contains three datums: <code>:valley</code> in the root vector, <code>:mountain</code> and <code>:river</code>  in the nested map. Each of those datums shares a path with a set-as-a-predicate in the specification. When Speculoos validates, it treats each set in the specification as a predicate because the corresponding element in the data is a scalar, not a set. In this example, <code>:valley</code> and <code>:mountain</code> are members of the <code>geography</code> set-predicate, whereas <code>:river</code> is not.</p><p>Speculoos performs the two modes in separate passes, so we may even use both set-as-a-predicate-mode and set-scalar-validation-mode during the same validation, as long as the predicates stay on their own side of the fence.</p><pre><code>(validate-scalars [42 #{:foo :bar :baz}]
                  [#{40 41 42} #{keyword?}])
;; => ({:datum 42, :path [0], :predicate #{40 41 42}, :valid? 42}
;;     {:datum #{:bar :baz :foo},
;;      :path {:path [1], :value #{:bar :baz :foo}},
;;      :predicate #function [clojure.core/keyword?],
;;      :valid? true})</code></pre><p>In this example, the predicate at index <code>0</code> of the specification is a set while the datum at same index of the data is <code>42</code>, a scalar. Speculoos uses the set-as-a-predicate mode. Since <code>42</code> is a member of <code>#{40 41 42}</code>, that datum validates as truthy. Because the data at index <code>1</code> is itself a set, Speculoos performs set-scalar-validation. The <code>keyword?</code> predicate is applied to each element of <code>#{:foo :bar :baz}</code> at index <code>1</code> and they all validate <code>true</code>.</p><p> Speculoos will also validate a set as whole during a collection validation operation. The rules are identical to how the other collections are validated: predicates from the specification are applied to the parent container in the data. Nested collections are validated by ordinal, regardless of intervening predicates in the parent collection. But let's not get bogged down in a textual description; let's look at some examples.</p><p>The simplest case involves validating a set as a collection. In Speculoos' collection validation, the predicates apply to collections, not scalars.</p><pre><code>(def count-3? #(= 3 (count %)))</code><br><code>(validate-collections #{:foo :bar :baz} #{count-3?})
;; => ({:datum #{:bar :baz :foo},
;;      :ordinal-parent-path [],
;;      :path [#function [documentation/count-3?]],
;;      :valid? true,
;;      :value #function [documentation/count-3?]})</code></pre><p>Our data is a set containing a trio of keywords. Instead of a scalar specification applied to each of the scalars, our predicate <code>count-3?</code> applies to the containing collection <code>#{:foo :bar :baz}</code>.</p><p>Another principle of collection validation is that Speculoos will apply any number of collection predicates to its parent container. We'll illustrate this by creating three predicates that cannot by tested by validating scalars alone; the collection as a whole must be validated.</p><pre><code>(def equal-kw-sym?
  #(= (count (filter symbol? %))
      (count (filter keyword? %))))</code><br><code>(def more-kw?
  #(< (count (filter symbol? %))
      (count (filter (complement symbol?) %))))</code><br><code>(def zero-int? #(empty? (filter int? %)))</code><br><code>(validate-collections #{:foo :bar :baz 'foo 'bar}
                      #{equal-kw-sym? more-kw? zero-int?})
;; => ({:datum #{:bar :baz :foo bar foo},
;;      :ordinal-parent-path [],
;;      :path [#function [documentation/zero-int?]],
;;      :valid? true,
;;      :value #function [documentation/zero-int?]}
;;     {:datum #{:bar :baz :foo bar foo},
;;      :ordinal-parent-path [],
;;      :path [#function [documentation/equal-kw-sym?]],
;;      :valid? false,
;;      :value #function [documentation/equal-kw-sym?]}
;;     {:datum #{:bar :baz :foo bar foo},
;;      :ordinal-parent-path [],
;;      :path [#function [documentation/more-kw?]],
;;      :valid? true,
;;      :value #function [documentation/more-kw?]})</code></pre><p>Speculoos applies all three collection predicates to the parent set. The set does indeed contain more keywords than non-keywords, the set does not contain any integers, but the set does not hold equal numbers of keywords and symbols.</p><p>Nested sets are validated according to Speculoos' conventions for all collections: predicates apply to their parents, and predicates may be interleaved among nested collections. Example time. Lots of collection predicates.</p><pre><code>(def count-5? #(= 5 (count %)))</code><code>(def last-is-55? #(= 55 (last %)))</code><code>(def middle-is-33? #(= 33 (get % 2)))</code><code>(def all-kw? #(every? keyword? %))</code><code>(def all-qual-kw? #(every? qualified-keyword? %))</code><code>(def all-sym? #(every? symbol? %))</code></pre><p>Our parent container will be a vector with integers in the first, third, and fifth slots, with two sets nested at the second and fourth spots. The first nested set contains three keywords, the second nested set contains three symbols. Our specification is a vector with collection predicates at the first, second, and fifth spots and sets nested at the third and fourth spots.</p><pre><code>(validate-collections
  [42 #{:foo :bar :baz} 33 #{'foo 'bar 'baz} 55]
  [count-5? last-is-55? #{all-kw? all-qual-kw?} #{all-sym?}
   middle-is-33?])
;; => ({:datum [42 #{:bar :baz :foo} 33 #{bar baz foo} 55],
;;      :ordinal-parent-path [],
;;      :path [0],
;;      :valid? true,
;;      :value #function [documentation/count-5?]}
;;     {:datum [42 #{:bar :baz :foo} 33 #{bar baz foo} 55],
;;      :ordinal-parent-path [],
;;      :path [1],
;;      :valid? true,
;;      :value #function [documentation/last-is-55?]}
;;     {:datum #{:bar :baz :foo},
;;      :ordinal-parent-path [0],
;;      :path [2 #function [documentation/all-kw?]],
;;      :valid? true,
;;      :value #function [documentation/all-kw?]}
;;     {:datum #{:bar :baz :foo},
;;      :ordinal-parent-path [0],
;;      :path [2 #function [documentation/all-qual-kw?]],
;;      :valid? false,
;;      :value #function [documentation/all-qual-kw?]}
;;     {:datum #{bar baz foo},
;;      :ordinal-parent-path [1],
;;      :path [3 #function [documentation/all-sym?]],
;;      :valid? true,
;;      :value #function [documentation/all-sym?]}
;;     {:datum [42 #{:bar :baz :foo} 33 #{bar baz foo} 55],
;;      :ordinal-parent-path [],
;;      :path [4],
;;      :valid? true,
;;      :value #function [documentation/middle-is-33?]})</code></pre><p>The three top-level predicates <code>count-5?</code>, <code>last-is-55?</code>, and <code>middle-is-33?</code> all apply to the root vector, regardless of their positions within the root vector. Two predicates contained in the first nested set, <code>all-kw?</code> and <code>all-qual-kw?</code>, apply to the first nested set <code>#{:foo :bar :baz}</code>, regardless of their index within the root vector. Likewise, the single predicate in the second set, <code>all-sym?</code>, applies to the second set <code>#{'foo 'bar 'baz}</code> in the data.</p><p>The order of collection predicates does not matter. Speculoos applies them all to the parent container. All the following produce functionally equivalent results.</p><pre><code>[<strong>count-5? last-is-55?</strong> #{all-kw? all-qual-kw?}<strong> middle-is-33?</strong> #{all-sym?}]</code><code>[#{all-kw? all-qual-kw?} <strong>count-5? last-is-55? middle-is-33?</strong> #{all-sym?}]</code><code>[#{all-kw? all-qual-kw?} #{all-sym?} <strong>count-5? last-is-55? middle-is-33?</strong>]</code></pre><p>Just like elements contained in a Clojure set, the validation results are the same, merely the ordering is different.</p></section><section id="path-spec"><h2>Un digestif</h2><p>Speculoos' performs scalar and collection validation by inferring the paths from the specification's structure: the predicate located at path <code>[2 :a 5]</code> in the specification will be validated against datum located at path<label class="margin-toggle sidenote-number" for="or-coll"></label><input class="margin-toggle" id="or-coll" type="checkbox"><span class="sidenote">Or, in a collection validation, its parent.</span></input><code> [2 :a 5]</code> within the data structure. Writing a specification involves composing a data structure that mimics the data so that the paths of the predicates correspond to the paths of the datums.</p><p>One alternative way to assign predicates to datums in a heterogeneous, arbitrarily-nested data structure would be to explicitly state the datum paths. In this scenario, we supply the validator an explicit path into the data for each predicate. Speculoos provides a utility to do this. It's somewhat tedious to use, but it is quite powerful. Let's pretend we'd like to specify the third item of a vector as an integer.</p><pre><code>(require '[speculoos.core :refer [validate-with-path-spec]])</code><br><code>(validate-with-path-spec [11 22 33]
                         [{:paths [[2]], :predicate int?}])
;; => ({:args (33),
;;      :paths [[2]],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true})</code></pre><p>Basically, we manually do the first half of Speculoos' validation. We feed the validator the path to the datum instead of asking it to infer it from the structure of the specification. In this example, the datum <code>33</code> is located at path <code>[2]</code>. <code>validate-with-path-spec</code> iterates through a sequence of maps, each map contains paths to datums, and predicates to apply. This utility offers great flexibility because in any validation, it will consume any number of datums and send it to a high-arity predicate. Let's see that in action.</p><pre><code>(defn all-increasing? [& args] (apply < args))</code><br><code>(validate-with-path-spec [11 22 33]
                         [{:paths [[0] [1] [2]],
                           :predicate all-increasing?}])
;; => ({:args (11 22 33),
;;      :paths [[0] [1] [2]],
;;      :predicate #function [documentation/all-increasing?],
;;      :valid? true})</code></pre><p>We establish a predicate that tests if a group of numbers is monotonically increasing. <code>validate-with-path-spec</code> extracts the three datums with the three paths contained at <code>:paths</code>. With those datums in hand, it invokes the predicate with a <code>(apply predicate args)</code> pattern. The datums are fed in the order of the <code>:paths</code> sequence. Let's feed them in reverse.</p><pre><code>(defn all-decreasing? [& args] (apply > args))</code><br><code>(validate-with-path-spec [11 22 33]
                         [{:paths [[2] [1] [0]],
                           :predicate all-decreasing?}])
;; => ({:args (33 22 11),
;;      :paths [[2] [1] [0]],
;;      :predicate #function [documentation/all-decreasing?],
;;      :valid? true})</code></pre><p>And indeed, provided in reverse order, the integers are monotonically decreasing. Let's really push it with a five-arity predicate.<label class="margin-toggle sidenote-number" for="crazy"></label><input class="margin-toggle" id="crazy" type="checkbox"><span class="sidenote">Admittedly, <code>crazy-predicate</code> would never be useful in an actual validation, but it does highlight how to use <code>validate-with-path-spec</code>. </span></input></p><pre><code>(defn crazy-predicate
  [w x y z coll]
  (and (= w (count coll))
       (= y (get coll x))
       (= z (last coll))))</code><br><code>(def crazy-data {:a 3, :b [55 77 99], :c [77 1 [99]]})</code><br><code>(get-in* crazy-data [:a]) ;; => 3</code><code>(get-in* crazy-data [:c 1]) ;; => 1</code><code>(get-in* crazy-data [:c 0]) ;; => 77</code><code>(get-in* crazy-data [:c 2 0]) ;; => 99</code><code>(get-in* crazy-data [:b]) ;; => [55 77 99]</code><br><code>(= 3 (count [55 77 99])) ;; => true</code><code>(= 77 (get [55 77 99] 1)) ;; => true</code><code>(= 99 (last [55 77 99])) ;; => true</code><br><code>(validate-with-path-spec crazy-data
                         [{:paths [[:a] [:c 1] [:c 0]
                                   [:c 2 0] [:b]],
                           :predicate crazy-predicate}])
;; => ({:args (3 1 77 99 [55 77 99]),
;;      :paths [[:a] [:c 1] [:c 0] [:c 2 0] [:b]],
;;      :predicate #function [documentation/crazy-predicate],
;;      :valid? true})</code></pre><p><code>crazy-predicate</code> receives five arguments: four scalars, and one collection, and performs three comparisons on the collection involving its length and the values at couple of indexes. We've sprinkled throughout <code>crazy-data</code> the arguments which <code>validate-with-path-spec</code> will pass to <code>crazy-predicate</code>. Referring to <code>crazy-predicate</code>'s argument list, we find the vector length <code>w</code> at path <code>[:a]</code>, the vector's last value <code>z</code> at <code>[:c 2 0]</code>, etc. <code>crazy-predicate</code> calculates that <code>[55 77 99]</code> contains three elements, it finds <code>77</code> at index <code>1</code>, and <code>99</code> is indeed the last datum, so <code>valid?</code> is <code>true</code>.</p><p>To validate more, supply more specification entries.</p><pre><code>(validate-with-path-spec [11 22 33]
                         [{:paths [[1]], :predicate int?}
                          {:paths [[2]], :predicate ratio?}
                          {:paths [[]],
                           :predicate vector?}])
;; => ({:args (22),
;;      :paths [[1]],
;;      :predicate #function [clojure.core/int?],
;;      :valid? true}
;;     {:args (33),
;;      :paths [[2]],
;;      :predicate #function [clojure.core/ratio?],
;;      :valid? false}
;;     {:args ([11 22 33]),
;;      :paths [[]],
;;      :predicate #function [clojure.core/vector?--5479],
;;      :valid? true})</code></pre><p><code>validate-with-path-spec</code> dutifully steps through each <code>:paths/:predicate</code> map of the specification sequence and applies each predicate to the datums it pulls out.</p><p><code>validate-with-path-spec</code> provides us with the flexibility of validating any number of scalar and/or collection datums with multi-arity predicates. Writing its specifications, however, is not compact nor expressive. I am still considering how we might improve that.</p></section><section id="glossary"><h2>Glossary</h2><dl><dt id="datums">datums</dt><dd>Usually, the plural of <em>datum</em> would be <em>data</em>, but there are scenarios when <em>data</em> implies too much. A <code>:name</code>, an <code>:email</code>, and a <code>:favorite-ice-cream</code> make a coherent grouping of <em>data</em> about a person. On the other hand, if you're watching a pipeline, and pulling out a singular piece of information and sending it off somewhere else, there's no apparent relationship (from your perspective) about each datum. But over time, you are handling multiple, singular pieces of information, <em>datums</em>. Similarly, a value passed to a function is a datum and a value returned from that function is also a datum, but together, they don't necessarily aggregate to <em>data</em> so I'd call the two <em>datums</em>. I use <em>datums</em> throughout the text of the Speculoos project because it makes sense to my weirdo brain.</dd><dt id="element">element</dt><dd>A thing contained within a collection, either a scalar value or another nested collection.</dd><dt id="HANDS">heterogeneous, arbitrarily-nested data structure</dt><dd>Exactly one Clojure collection (vector, map, list, or set) with zero or more elements, nested to any depth.</dd><dt id="non-term-seq">non-terminating sequence</dt><dd>One of <code>clojure.lang.{Cycle,Iterate,LazySeq,LongRange,Range,Repeat}</code> that may or may not be realized, and possibly infinite.<label class="margin-toggle sidenote-number" for="infinite?"></label><input class="margin-toggle" id="infinite?" type="checkbox"><span class="sidenote">I am not aware of any way to determine if such a sequence is infinite, so Speculoos treats them as if they are.</span></input></dd><dt id="path">path</dt><dd>A vector that unambiguously navigates to a single element (scalar or sub-collection) in a heterogeneous, arbitrarily-nested data structure. Almost identical to the second argument of <a href="https://clojure.github.io/clojure/clojure.core-api.html#clojure.core/get-in"><code>clojure.core/get-in</code></a>, but with more generality. In the context of the Speculoos project, you supply a <em>path</em> as the second argument to the starred functions <code>get-in*</code>, <code>assoc-in*</code>, <code>update-in*</code>, and <code>dissoc-in*</code>. Vector elements and list elements are indexed by zero-based integers. Map values are addressed by their keys, which are often keywords, but can be any data type, including integers, or composite types.<label class="margin-toggle sidenote-number" for="keys"></label><input class="margin-toggle" id="keys" type="checkbox"><span class="sidenote">You don&apos;t often need to key a map on a multi-element, nested structure, but when you need to, it&apos;s awesome.</span></input> Set members are addressed by their identities.<label class="margin-toggle sidenote-number" for="nested-sets"></label><input class="margin-toggle" id="nested-sets" type="checkbox"><span class="sidenote">Nested collections contained in a set can certainly be addressed; the path vector itself contains the collections.</span></input> An empty vector <code>[]</code> addresses the outermost, containing collection. <em>Path</em>s are used extensively in the implementation of Speculoos' validation functions, but also are crucial in the reports of invalid datums.</dd><dt>predicate</dt><dd>A function<label class="margin-toggle sidenote-number" for="IFn"></label><input class="margin-toggle" id="IFn" type="checkbox"><span class="sidenote">Or something that implements <code>IFn</code>, like a set.</span></input> that returns a truthy or falsey value.  In the vast majority of instances, a function of one argument, but in certain corners of Speculoos, such as in argument-vs-return specifications, the function consumes more than one argument.</dd><dt>scalar</dt><dd>A single, non-divisible datum, such as an integer, string, boolean, etc. Essentially, a shorter term for <em>non-collection</em>.<label class="margin-toggle sidenote-number" for="atom"></label><input class="margin-toggle" id="atom" type="checkbox"><span class="sidenote">I&apos;d prefer to have adopted <em>atom</em> in this role, but Clojure already uses it <a href="https://clojure.github.io/clojure/clojure.core-api.html#clojure.core/atom">elsewhere</a>.</span></input></dd><dt>validate</dt><dd>An action that returns an exhaustive listing of all datum+predicate pairs, their paths, and whether the datum satisfies the predicate.</dd><dt>valid?</dt><dd>Returns <code>true</code> if all datums satisfy their predicates, <code>false</code> otherwise.</dd></dl></section></article><p id="page-footer">Copyright © 2024 Brad Losavio.<br>Compiled 2024 July 24.<span id="uuid"><br>a944b037-0fba-43d6-b27b-c701f468ee2b</span></p></body></head></html>